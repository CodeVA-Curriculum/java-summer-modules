---
title: Objects
author: Jon Stapleton & Eva Anderson
date: 8/21/2022
type: page
---

# Object-Oriented Thinking

In your previous programs, you probably have created groups of variables and maybe even some methods that "belong" together. For example, if you were to create a tree (as we have done many times in this module), you might have two or three variables (e.g., `x`, `y`, `size`) along with some lines of code (e.g., `rect()`, `circle()`) to use those variables and display the tree. In programming, these lines of code represent a "thing"--the tree. Java has a way to express this "belonging together" relationship in code: objects.

You can think of objects as complex data types that encapsulate attributes and behaviors associated with a particular component in your program or its output. Like methods, object-oriented programming is another mechanism to modularize a larger program. With objects you can contain related variables and methods in one location to increase readability and reusability and also make testing and debugging easier. 

## Object Basics

Recall when we learned about variables that we delved into Java’s primitive data types, like `float` and `int` and also mentioned reference data types for objects. Objects are complex data types that are defined by you, the programmer, to help consolidate and manage information in a larger program. When defining an object, we consider what attributes that object *has* and what it can *do* or what we can do with it. A class structure holds the blueprint for the object and includes variables to hold information and methods to perform actions. The basics of object-oriented programming has two parts:

1. **Design and write the class**: define and write the object’s blueprint/cookie cutter/template (in a separate program file)
2. **Create and use objects**: Use the template to create objects and use them (in your main program file)

### Design and Write Class

In order to write a blueprint, first I need to find the object "hidden" in the code, and consider its attributes (i.e., variables) and behaviors (i.e., methods). Let’s use one of our earlier examples of drawing trees to see how the process works. Consider the following program:

:::code-example{src="TODO:"}
```java
// MAIN PROGRAM
// global variables
int green = color(46, 179, 93);
int brown = color(92, 81, 59);
float x = 200;
float y = 200;
float size = 100;

void setup() {
    size(400,400);

    // draw tree
    fill(brown);
    rect(x, y, size / 5, size);
    fill(green);
    circle(x+10, y+10, size);
}

void draw() {
    
}

```
:::

In Processing programs, objects (like the tree) displayed in the output are good candidates for creating Java objects in the code. The program above displays a tree that has attributes and performs actions, so we can define it as an object data type. What attributes does it have? It has an `x` and `y` coordinate and a `size`, as well as colors. What does it do in this program or should I plan to do with it? I can draw it on the screen.

Now, I can begin to write the code for the tree blueprint. First, I create a class with the `class` keyword, give the class a name, and add curly braces:

```java
// OBJECT BLUEPRINT
class Tree {

}
```

The name of the class is the name of my new data type. I will use it when I create objects, so I want to choose a descriptive and meaningful name. Class names, by convention, begin with an uppercase letter. The class holds variables and methods associated with the `Tree` object, so I’ll add those between the `{` and `}`. First the variables:

```java
// OBJECT BLUEPRINT
class Tree {
   // instance variables
   int green = color(46, 179, 93);
   int brown = color(92, 81, 59);
   float x = 200;
   float y = 200;
   float size = 100;

}
```

The variables are the attributes that I identified earlier and are often called **instance variables**. For the action I identified, I will add a custom method definition containing the code to display the object:

```java
// OBJECT BLUEPRINT
class Tree {
   int green = color(46, 179, 93);
   int brown = color(92, 81, 59);
   float x = 200;
   float y = 200;
   float size = 100;

   void display() {
      fill(brown);
      rect(x, y, size / 5, size);
      fill(green);
      circle(x+10, y+10, size);
   }
}
```

Our first class template is complete! This code may look similar to the program we started with or to programs we’ve written so far, but it does not behave the same. It has no `setup()` or `draw()` method and this code is not runnable on its own. The `class` keyword and it’s layout identifies it as a *template* for a `Tree` object. The code only executes when I call it to create a `Tree` object from the main program.

## Create and Use Objects

Once I have the template, I can call it to to create `Tree` objects in the "main" program by using the new data type (the class name), a variable name, and keyword `new`. The code looks slightly different than code we’ve used to declare and initialize variables before, and we will remove a lot of the code we had, so I’ll strikeout the old code to illustrate how much the program changes:

:::code-tabs
::name[Main]
```java
// MAIN PROGRAM
// global variables
int green = color(46, 179, 93);
int brown = color(92, 81, 59);
float x = 200;
float y = 200;
float size = 100;
Tree myTree; // declare Tree object variable 

void setup() {
    size(400,400);

    // draw tree
    fill(brown);
    rect(x, y, size / 5, size);
    fill(green);
    circle(x+10, y+10, size);

     myTree = new Tree(); // initialize the Tree object variable
}

void draw() {
    
}
```

::name[Tree]
```java
// OBJECT BLUEPRINT
class Tree {
   int green = color(46, 179, 93);
   int brown = color(92, 81, 59);
   float x = 200;
   float y = 200;
   float size = 100;

   void display() {
      fill(brown);
      rect(x, y, size / 5, size);
      fill(green);
      circle(x+10, y+10, size);
   }
}
```
:::


Removing the strikethrough code, I am left with:

:::code-tabs
::name[Main]
```java
// MAIN PROGRAM
// global variables
Tree myTree; // declare Tree object variable

void setup() {
    size(400,400);

    myTree = new Tree();  // initalize Tree object variable
}

void draw() {
    
}
```

::name[Tree]
```java
// OBJECT BLUEPRINT
class Tree {
   int green = color(46, 179, 93);
   int brown = color(92, 81, 59);
   float x = 200;
   float y = 200;
   float size = 100;

   void display() {
      fill(brown);
      rect(x, y, size / 5, size);
      fill(green);
      circle(x+10, y+10, size);
   }
}
```
:::

All the code related to creating and displaying a tree now resides in the `Tree` class, and we are left with two new lines of code:

```java
Tree myTree;            // declare Tree object variable
myTree = new Tree();    // initialize Tree object variable
```

The first statement declares a new variable named `myTree` whose data type is `Tree`, which matches the class name of the template I wrote. Then, I instantiate the variable by using its name, the assignment operator  (`=`), the keyword `new`, followed by the data type again with parenthesis after it. This final piece of code (`Tree()`) calls the Tree class **constructor**--we’ll talk more about in the next section. The statement above can also be rewritten in one line, just like other variables:

```java
Tree myTree = new Tree();
```

These sorts of lines of code can be a little confusing because you need to read the word `Tree` so many times. Each `Tree` in the above program has a different meaning:

* The first `Tree` is the data type of the variable `myTree`, letting the Java compiler know that the variable should only contain a `Tree` object (instead of an `int` or a `float` or whatever else)
* The next one, `myTree`, is the name of the variable that will store the `Tree` object
* Finally, `new Tree()` is the command that tells the computer to use the `Tree` class template to initialize a new object, and store it in the `myTree` variable. When `myTree` is **instantiated** (a special term for initializing objects), it receives a copy of all the variables declared in the object’s template and assigns them an initial value.

The computer will now compile and run the program, but it doesn't display anything yet. I have to add a statement to display the tree by calling the `display()` method with the object’s name and the dot operator, like so:

:::code-tabs{src="TODO:"}
::name[Main]
```java
// MAIN PROGRAM
// global variables
Tree myTree; // declare Tree object variable

void setup() {
    size(400,400);

     myTree = new Tree();  // initalize Tree object variable
}

void draw() {
     myTree.display();    // call display method with attributes of `myTree`    
}
```

::name[Tree]
```java
// OBJECT BLUEPRINT
class Tree {
   int green = color(46, 179, 93);
   int brown = color(92, 81, 59);
   float x = 200;
   float y = 200;
   float size = 100;

   void display() {
      fill(brown);
      rect(x, y, size / 5, size);
      fill(green);
      circle(x+10, y+10, size);
   }
}
```
:::

The line of code `myTree.display()` tells the computer to run the `display()` method defined in the `Tree` class template using the attributes from the `myTree` object. The placement of each line of code above is a common placement for declaring, initializing, and using objects and makes it easier to add animations later.

## You Try It!

Copy the main program and the Tree program into Processing:

Create a new program and copy the main code there

<!-- objects_main_program.PNG -->
![Processing window display with main sketch program that declares a Tree object variable called myTree that is instantiated by calling a no-arg constructor in the setup method and then is used to call the display method inside the draw method.](TODO:)

Click the down arrow to add the template code, name the program `Tree` (class name and file name must match, be sure to capitalize the first letter and use the same rules that you would when naming variables)

<!-- objects_new_tab.PNG -->
![Processing window display with main sketch program. The downward arrow to open a new sketch has been clicked and a pop-up window is displayed with New Tab highlighted.](TODO:)

<!-- objects_new_program.PNG -->
![Processing window display with main sketch program. A pop-up window is displayed with a prompt for the user to enter a name for the file and the user has typed in Tree for the name.](TODO:)

Copy the Tree program into this new tab

<!-- objects_object_template.PNG -->
![Processing window display with two tabs for main sketch program and Tree program. The Tree program is displayed. It is an object template with 5 instance variables: green, brown, x, y, and size. There is one display method that uses these values to display a circle and a rectangle.](TODO:)

Then, go back to your main program and run it to see the result.

<!-- objects_main_program_final.PNG -->
![Processing window display with two tabs for main sketch program and Tree program. The main program is displayed that creates a Tree object called myTree, instantiates it and uses it to call the display method.](TODO:)

Tinker with the code or try to erase and rewrite portions of it without looking to get used to the new syntax.


This first example demonstrates the basics of object-oriented programming. In subsequent sections we will build on these basics by implementing the following improvements:

* Tweak the template to be more abstract (remove hard-coded values)
* Create additional object variables with different attributes
* Write another object template and create objects with it
* Add methods to access and alter attributes of each object
* Add constructors to the template to initialize attributes

---