---
title: Multiple Objects and Setters
authors: Jon Stapleton & Eva Anderson
date: 8/21/2022
type: page
---

# Multiple Objects 

We ended the last section creating and displaying one `Tree` object using our new `Tree` class template. The main benefit of creating classes, however, is to *reuse* the template to create multiple objects. I can create a second `Tree` variable called `anotherTree` like so,

:::code-tabs{src="TODO:"}
::name[Main]
```java
// MAIN PROGRAM
// global variables
Tree myTree;
Tree anotherTree;

void setup() {
    size(400,400);

     myTree = new Tree();  
     anotherTree = new Tree();
}

void draw() {
     myTree.display();    
     anotherTree.display();    
}
```

::name[Tree]
```java
// OBJECT BLUEPRINT
class Tree {
   int green = color(46, 179, 93);
   int brown = color(92, 81, 59);
   float x = 200;
   float y = 200;
   float size = 100;

   void display() {
      fill(brown);
      rect(x, y, size / 5, size);
      fill(green);
      circle(x+10, y+10, size);
   }
}
```
:::

When I run this code (as you can see above), both trees will display on top of one another because they both have the same values for `x` and `y` and `size`. Each object gets its *own copy of the variables* for coordinates and size though; `myTree` has a set of variables for colors, coordinates, and size and `anotherTree` has its own set of those variables. So, I can change the values for one of the objects without changing the other. One way I can access the variables is by using the object name with the dot operator, followed by the variable name. Then I can use the assignment operator (`=`) to assign a new value:

:::code-tabs{serc="TODO:"}
::name[Main]
```java
// MAIN PROGRAM
// global variables
Tree myTree;
Tree anotherTree;

void setup() {
    size(400,400);

     myTree = new Tree();  
     anotherTree = new Tree();

     // change the attributes for ‘anotherTree’
     anotherTree.x = 100;    
     anotherTree.y = 100;
     anotherTree.size = 50;
}

void draw() {
     myTree.display();    
     anotherTree.display();    
}
```

::name[Tree]
```java
// OBJECT BLUEPRINT
class Tree {
   int green = color(46, 179, 93);
   int brown = color(92, 81, 59);
   float x = 200;
   float y = 200;
   float size = 100;

   void display() {
      fill(brown);
      rect(x, y, size / 5, size);
      fill(green);
      circle(x+10, y+10, size);
   }
}
```
:::

## Setters

The above draws a tree at `(200,200)` with size `100` (default values) and another tree at `(100,100)` with size `50`. The preferred approach with objects, though, is to use *methods* to access and change object variables rather than accessing instance variables directly. In order to use methods to change the coordinates and the size, I need three new methods in the template, one for each variable. First, I’ll add a method that can be called to change the x-coordinate:

```java
// OBJECT BLUEPRINT
class Tree {
   int green = color(46, 179, 93);
   int brown = color(92, 81, 59);
   float x = 200;
   float y = 200;
   float size = 100;

   void display() {
      fill(brown);
      rect(x, y, size / 5, size);
      fill(green);
      circle(x+10, y+10, size);
   }

   // method that can be used to change the value of ‘x’
   void setX(float tempX) {
      x = tempX;      // uses parameter value to set the object’s x-coordinate    
   }
}
```

Then, I’ll call the method in main:

```java
// MAIN PROGRAM
// global variables
Tree myTree;
Tree anotherTree;

void setup() {
    size(400,400);

     myTree = new Tree();  
     anotherTree = new Tree();

     // change the values for ‘anotherTree’
     anotherTree.setX(100);    // call to setX method
     anotherTree.y = 100;
     anotherTree.size = 50;
}

void draw() {
     myTree.display();    
     anotherTree.display();    
}
```

The call includes the object name and dot operator so the computer knows which object’s `x` value is being changed. Methods that change attributes of an object often use the word ‘set’ in the name and are referred to collectively as **setters**. This new solution with the method call adds more code and does not seem that different, but it is a preferred practice to use methods to access or change an object’s values.

Next, I will add setters for the y-coordinate and size and also change the main program to call these new methods. Check out both tabs in the code example below to see the new "setter" method definitions (in `Tree`) and the new method calls (in `Main`):

:::code-tabs{src="TODO:"}
::name[Main]
```java
// MAIN PROGRAM
// global variables
Tree myTree;
Tree anotherTree;

void setup() {
    size(400,400);

     myTree = new Tree();  
     anotherTree = new Tree();

     // call setters to change the values for ‘anotherTree’
     anotherTree.setX(100);    
     anotherTree.setY(100);
     anotherTree.setSize(50);
}

void draw() {
     myTree.display();    
     anotherTree.display();    
}
```

::name[Tree]
```java
// OBJECT BLUEPRINT
class Tree {
   int green = color(46, 179, 93);
   int brown = color(92, 81, 59);
   float x = 200;
   float y = 200;
   float size = 100;

   void display() {
      fill(brown);
      rect(x, y, size / 5, size);
      fill(green);
      circle(x+10, y+10, size);
   }

   // setters

   void setX(float tempX) {
      x = tempX;        // uses parameter value to set the object’s x-coordinate
   }

   void setY(float tempY) {
      y = tempY;        // uses parameter value to set the object’s y-coordinate
   }

   void setSize(float tempSize) {
      size = tempSize;     // uses parameter value to set the object’s size
   }
}
```
:::

I can call these new methods to change the coordinates or size for any objects I create. For example, I can change the size of the first tree by adding the following code:

:::code-tabs{src="TODO:"}
::name[Main]
```java
// MAIN PROGRAM
// global variables
Tree myTree;
Tree anotherTree;

void setup() {
    size(400,400);

     myTree = new Tree();  
     anotherTree = new Tree();

     // call setter to change the size of ‘myTree’
     myTree.setSize(200);

     // call setters to change the values for ‘anotherTree’
     anotherTree.setX(100);    
     anotherTree.setY(100);
     anotherTree.setSize(50);
}

void draw() {
     myTree.display();    
     anotherTree.display();    
}
```

::name[Tree]
```java
// OBJECT BLUEPRINT
class Tree {
   int green = color(46, 179, 93);
   int brown = color(92, 81, 59);
   float x = 200;
   float y = 200;
   float size = 100;

   void display() {
      fill(brown);
      rect(x, y, size / 5, size);
      fill(green);
      circle(x+10, y+10, size);
   }

   // setters

   void setX(float tempX) {
      x = tempX;        // uses parameter value to set the object’s x-coordinate
   }

   void setY(float tempY) {
      y = tempY;        // uses parameter value to set the object’s y-coordinate
   }

   void setSize(float tempSize) {
      size = tempSize;     // uses parameter value to set the object’s size
   }
}
```
:::

The object name before the dot operator indicates which object I want to change.

> **Try It!**: Add a setter that changes the green color and then call it to change the color of one of your trees. Test your program.

## Common Mistakes

* Calling a method with an object *before* it’s instantiated with the `new` keyword. This produces a `NullPointerException`.
* Using an assignment operator (`=`) when calling a setter method. This is improper syntax and will produce a compiler error. (The new value is passed as an argument inside parenthesis in the method call. The = sign is inside the setter method definition in the template.)
* Using incorrect variable names or method names or using them in the wrong location. Since there are a lot of variables names, parameters, and methods inside the object template as well as object names, it can be a lot to keep track of at first. (Use the sample code above as a reference)
* Thinking that *all* the code in the object template runs automatically. A template method runs when an object is created and then calls the method when it’s needed. Just because a template provides a method, doesn’t mean we have to call it. 
