---
title: Object Animations
author: Eva Anderson & Jon Stapleton
date: 8/21/2022
type: page
---

# Object Animations

Now that we have constructors to initialize an object, a method to display it, and setters to change it, let's create some animations. Recall that `setup()` is called once when we run our program and then `draw()` is called repeatedly until we stop the program. By changing an object right before we display it in the `draw()` method, we can create an animation.

For a quick animation, I’ll add a global variable for the current tree size. By slowly increasing this variable’s value and using it to set the Tree object’s size, I can simulate a tree "growing":

:::code-example{src="TODO:" tabs}
::name[Main]
```java
// MAIN PROGRAM
// global variables
Tree myTree;
float currentSize = 50;

void setup() {
    size(400,400);

     myTree = new Tree(currentSize);     
}

void draw() {
     currentSize = currentSize + 0.3;
     myTree.setSize(currentSize);
     myTree.display();    
}
```

::name[Tree]
```java
// OBJECT BLUEPRINT
class Tree {
   // object variables with default values
   int green = color(46, 179, 93);
   int brown = color(92, 81, 59);
   float x = 200;
   float y = 200;
   float size = 100;

   // 3-arg constructor omitted for space

   // 1-arg constructor
   Tree(float tempSize) {
       size = tempSize;
   }

   void display() {
      fill(brown);
      rect(x, y, size / 5, size);
      fill(green);
      circle(x+10, y+10, size);
   }

   // setters omitted for space
}
```
:::

Another solution is to add a method called `grow()` to my template that increases the Tree object’s size directly and call that method from main:

:::code-example{src="TODO:" tabs}
::name[Main]
```java
// MAIN PROGRAM
// global variables
Tree myTree;

void setup() {
    size(400,400);

     myTree = new Tree(50);     
}

void draw() {
     myTree.grow();  // call new method to change size
     myTree.display();    
}
```

::name[Tree]
```java
// OBJECT BLUEPRINT
class Tree {
   // object variables with default values
   int green = color(46, 179, 93);
   int brown = color(92, 81, 59);
   float x = 200;
   float y = 200;
   float size = 100;

   // 3-arg constructor omitted for space

   // 1-arg constructor
   Tree(float tempSize) {
       size = tempSize;
   }

   void display() {
      fill(brown);
      rect(x, y, size / 5, size);
      fill(green);
      circle(x+10, y+10, size);
   }

   // new method to increase tree size
   void grow() {
      size = size + 0.3;
   }

   // setters omitted for space
}
```
:::

Both solutions work, but if growing a tree is something I will do often, then it makes sense to add that functionality to the `Tree` class.

I can even *overload* the `grow()` method so the rate the tree grows can be changed and I can add code in both `grow()` methods so that a tree doesn’t go beyond a maximum size:

:::code-example{src="TODO:" tabs}
::name[Tree]
```java
// OBJECT BLUEPRINT
class Tree {
   // object variables with default values
   int green = color(46, 179, 93);
   int brown = color(92, 81, 59);
   float x = 200;
   float y = 200;
   float size = 100;

   // 3-arg constructor omitted for space
   // 1-arg constructor
   Tree(float tempSize) {
       size = tempSize;
   }

   void display() {
      fill(brown);
      rect(x, y, size / 5, size);
      fill(green);
      circle(x+10, y+10, size);
   }

   // new methods to increase tree size
   void grow() {
      if (size < 200)
      {
         size = size + 0.3;
      }
      // else 
      //     the tree is maximum size, so do nothing
   }

   void grow(float amount) {
      if (size < 200)
      {
         size = size + amount;
      }
      // else 
      //     the tree is maximum size, so do nothing
   }

   // setters omitted for space
}
```

::name[Main]
```java
// MAIN PROGRAM
// global variables
Tree myTree;

void setup() {
    size(400,400);

     myTree = new Tree(50);     
}

void draw() {
     myTree.grow(0.2);
     myTree.display();    
}
```
:::

The possibilities for designing objects and adding functionality with instance variables and methods are endless. 
As a final note, observe that in the above example, the `myTree` object is only calling the 1-arg constructor, a `grow()` method, and the `display()` method. Object templates provide choices with additional constructors and methods, but we only call what we need in order to accomplish our task.

---