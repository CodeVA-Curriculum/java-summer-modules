---
title: Constructors
author: Jon Stapleton & Eva Anderson
date: 8/21/2022
type: page
---

# Constructors

In the last section, I used setters to adjust initial values so I could create and display two `Tree` objects. There is a special method, called a **constructor**, that can I can create and call to set initial values for an object in one line of code. The computer runs the constructor when it creates an object with the `new` keyword. We can pass data to the constructor to set the value of the object’s instance variables. A constructor’s definition is slightly different than typical method definitions:

* A constructor header has no return type at all (not even `void`)
* The constructor’s name must match the class name *exactly*
* Constructors are frequently *overloaded* to allow flexibility in initializing objects

I’m going to create a constructor in `Tree` to set the `x` and `y` coordinates as well as the size of the tree when I instantiate it. First, I add the constructor to the template with parameters to receive the initial values:

```java
// OBJECT BLUEPRINT
class Tree {
   // object variables with default values
   int green = color(46, 179, 93);
   int brown = color(92, 81, 59);
   float x = 200;
   float y = 200;
   float size = 100;

   // constructor
   Tree(float tempX, float tempY, float tempSize) {
       x = tempX;
       y = tempY;
       size = tempSize;
   }

   void display() {
      fill(brown);
      rect(x, y, size / 5, size);
      fill(green);
      circle(x+10, y+10, size);
   }

   // setters
   void setX(float tempX) {
      x = tempX;        // uses parameter value to set the object’s x-coordinate
   }

   void setY(float tempY) {
      y = tempY;        // uses parameter value to set the object’s y-coordinate
   }

   void setSize(float tempSize) {
      size = tempSize;     // uses parameter value to set the object’s size
   }
}
```

The constructor name `Tree` matches the class name exactly, and there is no return type. The computer uses the three parameters `tempX`, `tempY`, and `tempSize` to set the values of the object variables `x`, `y`, and `size`. Recall that the computer creates parameters when it calls the method, and destroys them at the end of the method. Global variables, however, like the instance variables `x`, `y`, and `size`, are permanent variables that persist in each object.

Next, I’ll change the main program to remove all the calls to the setters and call the new 3-argument constructor instead:

<!-- another-tree.png -->
:::code-example{src="67702841" tabs}
::name[Main]
```java
// MAIN PROGRAM
// global variables
Tree myTree;
Tree anotherTree;

void setup() {
   size(400,400);

   // myTree = new Tree();  // old no-arg constructor
   anotherTree = new Tree(100, 100, 50); // calls 3-arg constructor

   // call setter to change the size of ‘myTree’
   // myTree.setSize(200); // remove
}

void draw() {
   // myTree.display();
   anotherTree.display();    
}
```

::name[Tree]
```java
// OBJECT BLUEPRINT
class Tree {
   // object variables with default values
   int green = color(46, 179, 93);
   int brown = color(92, 81, 59);
   float x = 200;
   float y = 200;
   float size = 100;

   // constructor
   Tree(float tempX, float tempY, float tempSize) {
       x = tempX;
       y = tempY;
       size = tempSize;
   }

   void display() {
      fill(brown);
      rect(x, y, size / 5, size);
      fill(green);
      circle(x+10, y+10, size);
   }

   // setters
   void setX(float tempX) {
      x = tempX;        // uses parameter value to set the object’s x-coordinate
   }

   void setY(float tempY) {
      y = tempY;        // uses parameter value to set the object’s y-coordinate
   }

   void setSize(float tempSize) {
      size = tempSize;     // uses parameter value to set the object’s size
   }
}
```
:::

I can still call setters later if I want to change values, but the constructor allows me to easily initialize several values at one time. I can call the 3-arg constructor to instantiate `myTree` too:

<!-- obj-two-trees.png -->
:::code-example{src="67702746" tabs}
::name[Main]
```java
// MAIN PROGRAM
// global variables
Tree myTree;
Tree anotherTree;

void setup() {
    size(400,400);

     myTree = new Tree(200, 200, 200);       // calls 3-arg constructor
     anotherTree = new Tree(100, 100, 50); // calls 3-arg constructor

}

void draw() {
     myTree.display();    
     anotherTree.display();    
}
```

::name[Tree]
```java
// OBJECT BLUEPRINT
class Tree {
   // object variables with default values
   int green = color(46, 179, 93);
   int brown = color(92, 81, 59);
   float x = 200;
   float y = 200;
   float size = 100;

   // constructor
   Tree(float tempX, float tempY, float tempSize) {
       x = tempX;
       y = tempY;
       size = tempSize;
   }

   void display() {
      fill(brown);
      rect(x, y, size / 5, size);
      fill(green);
      circle(x+10, y+10, size);
   }

   // setters
   void setX(float tempX) {
      x = tempX;        // uses parameter value to set the object’s x-coordinate
   }

   void setY(float tempY) {
      y = tempY;        // uses parameter value to set the object’s y-coordinate
   }

   void setSize(float tempSize) {
      size = tempSize;     // uses parameter value to set the object’s size
   }
}
```
:::


## Overloading Constructors

Notice that by using the 3-arg constructor, `myTree` will *reset* the x and y coordinates to `200`, even though they already had those values as a default.  I can overload the constructor in `Tree` by adding a 1-arg constructor that only changes the size, like so:

```java
// OBJECT BLUEPRINT
class Tree {
   // object variables with default values
   int green = color(46, 179, 93);
   int brown = color(92, 81, 59);
   float x = 200;
   float y = 200;
   float size = 100;

   // 3-arg constructor
   Tree(float tempX, float tempY, float tempSize) {
       x = tempX;
       y = tempY;
       size = tempSize;
   }

   // 1-arg constructor
   Tree(float tempSize) {
       size = tempSize;
   }

   // ...
   // rest of class omitted
}
```

And, then I can call the 1-arg constructor for `myTree`:

<!-- obj-two-trees.png -->
:::code-example{src="67702746" tabs}
::name[Main]
```java
// MAIN PROGRAM
// global variables
Tree myTree;
Tree anotherTree;

void setup() {
    size(400,400);

     myTree = new Tree(200);       // calls 1-arg constructor
     anotherTree = new Tree(100, 100, 50); // calls 3-arg constructor

}

void draw() {
     myTree.display();    
     anotherTree.display();    
}
```

::name[Tree]
```java
// OBJECT BLUEPRINT
class Tree {
   // object variables with default values
   int green = color(46, 179, 93);
   int brown = color(92, 81, 59);
   float x = 200;
   float y = 200;
   float size = 100;

   // 3-arg constructor
   Tree(float tempX, float tempY, float tempSize) {
       x = tempX;
       y = tempY;
       size = tempSize;
   }

   // 1-arg constructor
   Tree(float tempSize) {
       size = tempSize;
   }

   void display() {
      fill(brown);
      rect(x, y, size / 5, size);
      fill(green);
      circle(x+10, y+10, size);
   }

   // setters

   void setX(float tempX) {
      x = tempX;        // uses parameter value to set the object’s x-coordinate
   }

   void setY(float tempY) {
      y = tempY;        // uses parameter value to set the object’s y-coordinate
   }

   void setSize(float tempSize) {
      size = tempSize;     // uses parameter value to set the object’s size
   }
}
```
:::

So, for `myTree`, we use the default values for the colors and coordinates, but the `size` is changed to `200`.

> **Try It!**: Add a new 2-arg constructor that changes the x and y coordinates for a new object. Then create a new Tree object in the main program called `tempTree` that calls the 2-arg constructor with coordinates (150, 300). Don’t forget to add a call to display your new object.

At this point, we have constructors in our template to initialize objects, a method to display the object, and setters to change the object. In the next section, we’ll use setters and other methods to create animations with our objects.

---
