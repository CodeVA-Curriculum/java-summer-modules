---
title: Custom Methods With Parameters
author: Eva Anderson
date: 7/6/2022
type: page
---

# Parameters

You are already familiar with parameters—your programs have made use of Processing methods with parameters like `rect(400, 300, 20, 100)` and `size(800, 600)`. These methods are defined in the Processing engine, so you haven’t had to worry about creating your own methods with parameters yet. A well-defined method performs a specific task and includes enough abstraction to be reused by your program and even other programs. Parameters allow you to provide input to a method, so including them in your method definitions allow you to create methods that are modular and reusable (i.e., more abstract).

## Using Parameters

In the Custom Methods module, we created a `drawTree()` method with no parameters and called it like so:

<!-- method-tree.png -->
:::code-example{src="67519553" alt="The output of the code above"}
```java
// colors
int yellow = color(245, 220, 32);
int green = color(46, 179, 93);
int brown = color(92, 81, 59);

void setup() {
    size(400,400);
    drawTree();
}

void draw() {
    
}

void drawTree() {
    fill(brown);
    rect(200, 200, 20, 100);
    fill(green);
    circle(210, 210, 100);
}
```
:::

This method always directs the computer to draw the tree at the same location on the screen, though. Right now, you need to define the location of the tree (in this case, `400, 300`) inside the method definition. This **hard-coded** value means that even if you write many calls to the `drawTree()` method in your program, the tree will always appear in the center of the screen. Here’s another version that works the same way, but with variables to hold the `x` and `y` location of the tree:

```java
void drawTree() {
    float x = 200;
    float y = 200;

    fill(brown);
    rect(x, y, 20, 100);
    fill(green);
    circle(x+10, y+10, 310, 100);
}
```

In the example above, the program assigns the variables `x` and `y` their values as part of the method *definition*. Parameters are special variables whose values are “passed” from the main program into the method, allowing me to set the value of variables inside the method definition as part of the method *call*. In the program below, I’ve changed the `float` variables `x` and `y` to be **parameters** instead of the usual local variables:

```java
// colors
int yellow = color(245, 220, 32);
int green = color(46, 179, 93);
int brown = color(92, 81, 59);

void setup() {
    size(400,400);
    drawTree();
}

void draw() {
    
}

void drawTree(float x, float y) {
    // moved the variables into the parentheses as parameters
    fill(brown);
    rect(x, y, 20, 100);
    fill(green);
    circle(x+10, y+10, 100);
}
```
The method header now includes two parameters (`float x` and `float y`), and the method call now includes two arguments (`400` and `300`) for those parameters. The parameters, `x` and `y`, are local variables available for use in the `drawTree()` method only. The computer creates the variables `x` and `y` when it reads the `drawTree(400, 300)` method call,  sets the value of the parameters based on the arguments in the call (`400` and `300`), and then deletes them from memory when it finishes running the `drawTree()` method. 

Now, I can reuse the `drawTree(float x, float y)` method to direct the computer to draw other trees by simply adding more `drawTree()` calls with different values for arguments. For example:

<!-- method-tree-three.png -->
:::code-example{src="67519552" alt="The output of the code above"}
```java
void setup() {
    size(400,400);
    drawTree(200, 200);
    drawTree(300, 120);
    drawTree(150, 75);
}
```
:::

Or I can use a loop to generate random values for arguments,

<!-- method-tree-random.png -->
:::code-example{src="67519551" alt="The output of the code above"}
```java
void setup() {
    size(800,600);
    int count = 0;
    while(count < 5) {
        float x = random(100, 700);
        float y = random(300, 500);
        drawTree(x, y);
        count = count + 1;
    }
}
```
:::

The `x` and `y` variables created in the loop above are "local" to that loop, and the computer will create and destroy them with each iteration of the loop. It is important to note that when the computer executes the `drawTree(x, y)` method, the computer passes the **value** of each variable to the parameters in the method, not the variable itself. It is also important to understand that the name of the variables in the method call do not have to match the name of the parameters; only their data types need to match.

I can also add a `mousePressed()` method and draw trees with a click of a button,

<!-- method-click.gif -->
:::code-example{src="67519550" alt="The output of the code above"}
```java
void mousePressed() {
     drawTree(mouseX, mouseY);
}
```
:::

The `drawTree()` method performs one task—drawing a tree. Because the method only does this one thing, and because I can provide *input* to the method using parameters, I can reuse the method in a lot of situations. Computer scientists call methods like this one **abstract** because it directs the computer to complete a task that is self-contained and reusable and each execution is different, depending on the value of the parameters. The `drawTree()` method is now similar to the `circle()` and `rect()` methods. I could even add another parameter to set the size of each tree or to select different types of trees.
 
## Why Use Parameters?

When first learning about parameters, they can seem like an unnecessary addition to code, making extra variables that complicate things. Remember that the goal with methods is to contain tasks to aid in repetition and to improve readability (for you, the programmer), especially in larger programs. The more self-contained a method is (using local variables and parameters), the easier it is to reuse it by calling it, not only in your program, but from other programs. For example, all the Processing methods we’ve used, like `circle()` and `rect()` reside in another program, so our programs have been calling methods that reside in another program all along.

Self-contained methods also make it easier to trace code. To begin to understand how parameters provide versatility and make debugging easier, consider the code below that uses global variables `x` and `y` instead of using parameters,

```java
// colors
int yellow = color(245, 220, 32);
int green = color(46, 179, 93);
int brown = color(92, 81, 59);

// global variable coordinates
float x;
float y;

void setup() {
    size(800,600);
    x = 400;
    y = 300;
    cloud();
    mountain();
    house();
    sun();
    drawTree();
}

void draw() {
    
}

void cloud() {
    /* method definition not shown */
}

void mountain() {
    /* method definition not shown */
}

void house() {
    /* method definition not shown */
}

void sun() {
    /* method definition not shown */
}

void drawTree() {
    fill(brown);
    rect(x, y, 20, 100);
    fill(green);
    circle(x+10, y+10, 100);
}
```

Imagine there is an error and the tree is not being drawn where you expected. With global variables and the code above, any method could’ve changed the values of `x` and `y` before the `drawTree()` method is called. I would have to search the cloud, mountain, house, and sun methods to see if one of them inadvertently changed the values of `x` and `y`. Of course, I can simply add code to “reset” the `x` and `y` values before calling the `drawTree()`method, but by using parameters, I avoid that whole process and control the input value with the method call itself and the method definition. (Note that this would require making local variables, because global variables should not be used as arguments in a method call.) Then, if a tree is in the wrong spot, I know that either the method call is incorrect or the method definition has an error; there is no need to search through other methods or the rest of our program to find the error. Additionally, if I want to change *how* the program draws trees, I only have to change one part of the code because all of the code that has to do with trees is isolated.

## Constants

You may have noticed that the colors for the tree are global variables, not local values. Why didn’t I make parameters for colors? Since the value for specific colors are not changed later in the program, this is an acceptable use of global variables. In fact, this variable can be declared as a constant using the keyword `final` which will ensure that it cannot be changed later by the program.

```java
// colors as constants
final int yellow = color(245, 220, 32);
final int green = color(46, 179, 93);
final int brown = color(92, 81, 59);

```

> **Practice:** Try It! Make the color variables into constants in one of your programs by adding the keyword `final`. Then add code in setup to try and change the value of the color. Run the program to see the compiler error you receive when trying to change a constant’s value.

I can also create `color` variables inside the method so the program doesn't rely on global variables at all and *all*; the data and functionality for the method is contained (or *encapsulated* inside the method.

<!-- method-tree.png -->
:::code-example{src="67519553" alt="The output of the code above"}
```java
// color constants
final int yellow = color(245, 220, 32);
final int green = color(46, 179, 93);
final int brown = color(92, 81, 59);

void setup() {
    size(400,400);
    drawTree(200, 200);
}

void draw() {
    
}

void drawTree(float x, float y) {
    int green = color(46, 179, 93);
    int brown = color(92, 81, 59);   
    fill(brown);
    rect(x, y, 20, 100);
    fill(green);
    circle(x+10, y+10, 100);
}
```
:::

Java always "looks" for a local variable to use first. So, when executing the `drawTree()` method in this example, Java will direct the computer to use the local values for `green` and `brown` instead of the global ones. If we change the values for `green` or `brown` in the global constants, it would not change the tree colors or change the “color” variables inside the `drawTree()` method (and vice versa). You can see the importance of knowing *where* a variable’s type is declared in order to trace the program code and you can also see why tracing variables can get tricky. 

With this last solution, where all the variables used by the `drawTree()` method are *declared* in the tree method, programs outside of your program can call `drawTree()` without needing additional information and without worrying about inadvertently interfering with its values in an unrelated part of the program.
