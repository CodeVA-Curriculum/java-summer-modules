---
title: Module 8 Mini-Project
author: Eva Anderson
date: 7/6/2022
type: assignment
---

# Module 8 Mini-Project

In this project, you'll practice writing methods with parameters to decompose code into functional components. There are two parts to the project; good luck!

## Part 1

I've writte the code below to draw a skyline of buildings against a black background. The code does not run yet, because you have to write the two overloaded building methods. Each building is a gray rectangle with upper left-hand coordinate (x,y) and some width (either hard-coded or random).. 

**Copy the starter code into your Processing environment.** 

```java
void setup() {
  size(800, 600);
  background(0);

  // draw buildings along the skyline 
  // add more calls to draw more buildings
  building(100, 200);        // draw building at (100,200) with random width
  building(200, 300, 150);    // draw building at (200,300) with width 150
}

void draw() {
}

// write the two building methods here:
```

Write one method, add additional method calls, and run and test your code. Then, write the other method and test it. For an *Optional Challenge*, try adding windows to the bulding.

Here's a sample output. Your output does not have to look like this exactly, but feel free to use it as a guide:

![alt](TODO:)

## Part 2

Add another element to your program using a new method! You can come up with your own idea or try one of these suggestions:

* **Mild:** Add a loop to draw buildings with random values for the height, width, or color.

* **Medium:** Add a `mousePressed()` method so the program adds a new building wherever the mouse is clicked.

* **Spicy:** Add a method to draw a specific number of stars in the background, where the number of stars is set by a parameter value.

Be sure to use comments and descriptive variable names to help label the different parts of your program!

## Submission Instructions

When you've completed your program, use the text entry on this page to submit the following information (in this order, if you can):

1. A screenshot of your program's output
2. Your code (just copy and paste it into the text entry area)

## Rubric

Here's a rubric for you to use as you self-assess your work:

| Criteria  | Goal | Stretch Goal |
| --------- | -------- | ---------- |
| Methods   | Your program makes use of a variety of methods, including repeated methods with different parameters | Your program uses methods not covered in the module, indicating advanced use of the Processing reference materials |
| Custom Methods | Your program makes use of one or more custom methods to display composite figures (i.e., visual elements made up of more than one primitive shape) | Your program makes use of several custom methods, each with a particularly elegant functional component purpose in the design of your program |
| Expressiveness | Your program uses comments to describe some sections of code and uses descriptive variable names, but it is difficult to discern the program's functionality without tracing it | Your program uses comments and variable names that accurately describe the purpose of each section of code |

If you'd like to see an example of a completed project, click *Reveal Example* below:

:::collapse{title="Reveal Example"}

::::code-example{src="TODO:"}
```java
void setup() {
  size(800, 600);
  background(0);
  drawStars(500);
  drawRandomBuildings(25);
}

// call the drawBuilding method with random values ‘n’ times
void drawRandomBuildings(int n) {
  // draw 25 buildings along the skyline
  float count=1;
  while (count <= n) {
    float x=random(0, 750);
    float y=random(100, 450);
    drawBuilding(x, y); // Draw a building
    count = count + 1;
  }
}

void draw() {  
}

// draw ‘count’ number of randomly-sized stars in random locations
void drawStars(int count) {
  int n=1;
  while (n <= count) {
    float x=random(5, 795);
    float y=random(5, 595);
    float size=random(2, 5);
    fill(255);
    ellipse(x, y, size, size); // Draw a star
    n = n + 1;
  }
}

// draw one gray building at (x,y) with width d
void drawBuilding(float x, float y, float d) {
  // draw building
  float h = 600-y; // height of building
  fill(getBuildingColor()); // shade of gray
  rect(x, y, d, h);

  // draw windows
  fill(150, 150, 10); // yellow
  float size = random(3,8); // side length of windows
  float offset = 5; // spacing between windows
  float x1 = x + offset; // coordinate for a window (x1,y1)
  float y1 = y + offset;
  while ( x1 + size < x + d ) { // draw one column at a time
    while ( y1 + size < y + h ) {
      rect(x1, y1, size, size);
      y1 = y1 + size + offset;
    }
    x1 = x1 + size + offset;
    y1 = y + offset;
  }
}

// draw one gray building at (x,y) with random width
void drawBuilding(float x, float y) {
  float w = random(25, 150);
  drawBuilding(x, y, w); // call other building method
}

// return a random gray color
int getBuildingColor() {
  int gray = (int)random(50, 150);
  return gray;
}

// draw one building with the click of a mouse (random width)
void mousePressed() {
  drawBuilding(mouseX, mouseY);
}

```
::::

You can also check out this more complex example of skyline design:
<https://imaginary-institute.com/previews/preview-skyline.html>

:::



