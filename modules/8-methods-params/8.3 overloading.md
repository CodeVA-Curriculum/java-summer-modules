---
title: Overloading Methods
authors: Eva Anderson
date: 7/6/2022
type: page
---

# Overloading Methods

There are times you might want to add parameters to an existing method to make it more flexible, but you may also want to keep the original method with fewer parameters too. In Java, you *can* reuse a method name for additional method definitions, as long as you vary the number or type of parameters each method receives. This is called overloading a method. Overloading increases the versatility of a method because it provides additional ways to call the method with varying number or types of arguments. Let’s look at an example:

Earlier, I created one `drawTree()` method with parameters for `x` and `y` coordinates. 

:::code-example{src="TODO:"}
```java
// colors
final int yellow = color(245, 220, 32);
final int green = color(46, 179, 93);
final int brown = color(92, 81, 59);

void setup() {
    size(800,600);

    // call tree method
    drawTree(150, 100); 
    drawTree(300, 100);  
    drawTree(450, 100);
}

void draw() {
}

// draw one tree at (x,y)
void drawTree(float x, float y) {
    fill(brown);
    rect(x, y, 20, 100);
    fill(green);
    circle(x+10, y+10, 100);
}
```
:::

I could’ve included an additional parameter `h` to set the size of the tree, like so:

```java
// colors
final int yellow = color(245, 220, 32);
final int green = color(46, 179, 93);
final int brown = color(92, 81, 59);

void setup() {
    size(800,600);

    // call tree method with size
    drawTree(100, 300, 150);  
    drawTree(300, 300, 200); 
    drawTree(600, 300, 250);
}

void draw() {  
}

// draw one tree at (x,y) with size h
void drawTree(float x, float y, float h) {
    if (h < 5) {
        h = 5;
    }
    fill(brown);
    rect(x, y, h / 5, h);
    fill(green);
    circle(x+h/10, y+h/10, h);
}
```

But what if I want the flexibility to call the `drawTree()` method either way, with or without specifying the size of the tree? No problem! I simply add *both* method definitions to my program:

:::code-example{src="TODO:"}
```java
// colors
final int yellow = color(245, 220, 32);
final int green = color(46, 179, 93);
final int brown = color(92, 81, 59);

void setup() {
    size(800,600);

    // call tree method without size
    drawTree(150, 100); 
    drawTree(300, 100);  
    drawTree(450, 100);

    // call tree method with size
    drawTree(100, 300, 150);  
    drawTree(300, 300, 200); 
    drawTree(600, 300, 250);
}

void draw() {  
}

// draw one tree at (x,y)
void drawTree(float x, float y) {
    fill(brown);
    rect(x, y, 20, 100);
    fill(green);
    circle(x+10, y+10, 100);
}

// draw one tree at (x,y) with size h
void drawTree(float x, float y, float h) {
    if (h < 5) {
        h = 5;
    }
    fill(brown);
    rect(x, y, h / 5, h);
    fill(green);
    circle(x+h/10, y+h/10, h);
}
```
:::

The `drawTree()` method is now *overloaded* in the program. There is a `drawTree()` method with two `float` parameters and a `drawTree()` method with three `float` parameters. Whenever the method is called, the computer *knows* which code to execute based on the number of arguments in the method call. As long as the number and type of arguments matches the parameters in a method definition, in the same order, the computer will know what to do. 

One caveat when overloading methods is that each method definition needs to be unique, so the computer knows for sure which method to execute. For example, I could not add the following method to the program above because it would conflict with the other two-float-argument `drawTree()` method.

```java
// draw one tree at specified y-coordinate with size h 
// (x-coordinate generated randomly)
void drawTree(float y, float h) {
   float x = random(0, 800); 
   if (h < 5) {
        h = 5;
    }
    fill(brown);
    rect(x, y, h / 5, h);
    fill(green);
    circle(x+h/10, y+h/10, h);
}
```

I *could* add a two-argument `drawTree()` method that receives a `String` and a `float` though, with the header `void drawTree(String m, float x)` and another one with the header `void drawTree(float x, String m)`, because a `String` and a `float` argument look different to the computer and it will use the corresponding method. The key is that for each overloaded method, the number of arguments needs to be unique, or the data types/order of data types needs to be unique.

## Errors

The Java compiler produces an error if it cannot find a method definition to match a method call. For example,

## Practice

Given the following method headers:

```java
void drawHouse(float x, float y, int w) {
    // code not shown
}

void displayMessage(String m) {
    // code not shown
}

void displayMessage(String m, int c) {
    // code not shown
}
```

Find the three method calls that are syntactically correct and determine the error in the other method calls:

```java
drawHouse(20, 30, 40.5);    // 1
drawHouse(35, 55);          // 2
drawHouse(22, 35.5, 42);    // 3
displayMessage(3, “Hello”); // 4
displayMessage(“Howdy”);    // 5
displayMessage(“Hiya”, “3”);// 6
displayMessage(“Aloha”, 7); // 7
displayMessage(Hola);       // 8
displayMessage();           // 9
```    

Once you think you have it, you can click "Reveal Solution" below to see the answers:

:::collapse{title="Reveal Solution"}
```java
drawHouse(20, 30, 40.5);    // 1 - 3rd argument must be an integer value, not a float
drawHouse(35, 55);        // 2 - 3 arguments are required
drawHouse(22, 35.5, 42);      // 3 - Correct!
displayMessage(3, “Hello”);    // 4 - The order of arguments matter; must be String then integer
displayMessage(“Howdy”);    // 5 - Correct!
displayMessage(“Hiya”, “3”);  // 6 - 2nd argument must be an integer value, not a String
displayMessage(“Aloha”, 7);  // 7 - Correct!
displayMessage(Hola);    // 8 - String literals must be surrounded by quotation marks
displayMessage();        // 9 - missing arguments
```
:::
