---
title: Custom Methods with Return Value
author: Eva Anderson
date: 7/6/2022
type: page
---

# Return Values

In past modules, you have used two Processing methods that “return” a value, `color()` and `random()`. These methods produce a value that you can then store in a variable and use elsewhere in your program:

```java
size(100, 100);
int y = random(0, 100); // produce a random value
ellipse(50, y, 20, 20);
```

“Returning” a value from a method is “output” from the method that can be used elsewhere in your program.
Just like with parameters, you’ve *used* methods that return a value, but you haven’t defined your own custom methods that return a value yet. Methods that return a value, include a “return” statement with the `return` keyword followed by a single value to be “returned”. The statement can be anywhere within the method and it indicates an immediate end to the method. 

## Using Return Values

Java methods can have zero, one, two, or more input parameters, but a method can only have zero or one return type. In the previous module, we created `void` methods which indicate that the method has no return type and does not return any value. To add a return value to a custom method, simply change the word `void` to match the *data type* of the value you’d like the method to return. Also, add a `return` statement followed by a value inside the method definition.

Consider the program below that draws trees with different shades of green and with different sizes so that “closer* trees (larger y-coordinate) appear bigger:

:::code-example{src="TODO:"}
```java
void setup() {
  size(800, 600);

  // draw trees
  drawTree(300, 100);
  drawTree(500, 100);
  drawTree(200, 200);
  drawTree(500, 200);
  drawTree(300, 300);
  drawTree(400, 300);
  drawTree(200, 400);
  drawTree(600, 400);
}

void draw() {
}

// draw one tree at (x,y)
void drawTree(float x, float y) {
  int brown = color(92, 81, 59);

  float vary = random(0.2, 0.8); // variable used to produce shades of green
  int green = color(46*vary, 179*vary, 93*vary); // produces different shade each time

  float percent = y / 500; // determines proximity of y-coordinate
  float h = 25 + percent * 150; // uses proximity to calculate tree size

  fill(brown);
  rect(x, y, h / 5, h);
  fill(green);
  circle(x+h/10, y+h/10, h);
}
```
:::

The program works as intended and all the code for tree is contained in one method, but there are a lot of calculations mixed together that make the method harder to read. Methods that return values are a great solution for encapsulating calculations and making code more readable. To create the method, choose a name for it and determine which variables can be created locally and which should be passed as parameter values in order to perform the calculation. Also determine the data type for the one return value. 

The program above used a calculation to generate a different shade of green for each tree. Let’s put that calculation in a method called `getTreeColor()`. The calculation uses `vary` for the calculation, but we can declare that variable inside the new method so the method will not require any input parameters. The output will be an integer value representing a shade of green, so the method header will specify `int` instead of `void` so I can return the color back to the `drawTree()` method:

```java
int getTreeColor() {

}
``` 

Next, I’ll copy the calculation to the new method and double check that all the variables are declared locally and that the calculation looks complete,

```java
int getTreeColor() {
  float vary = random(0.2, 0.8); // variable used to produce shades of green
  int green = color(46*vary, 179*vary, 93*vary); // produces different shade each time
}
``` 

I will add a return in the method to return the calculated value and double check that its type matches the return type on the method header:

```java
int getTreeColor() {
  float vary = random(0.2, 0.8); // variable used to produce shades of green
  int green = color(46*vary, 179*vary, 93*vary); // produces different shade each time
  return green; // is `green` an int?
}
``` 

Finally, I’ll add the method call with the correct arguments, if any, to the original method:

```java
// draw one tree at (x,y)
void drawTree(float x, float y) {
  int brown = color(92, 81, 59);

  int green = getTreeColor(); // new method call

  float percent = y / 500; // determines proximity of y-coordinate
  float h = 25 + percent * 150; // uses proximity to calculate tree size

  fill(brown);
  rect(x, y, h / 5, h);
  fill(green);
  circle(x+h/10, y+h/10, h);
}

```

Java does not require me to do anything with the returned value, but when methods return a value, we usually use the value immediately or store it in a variable for later use. When storing it in a variable, its type must be compatible with the return type on the method; the type is important, not the particular name of the variable. Here we stored the result returned by `getTreeColor()` in the `green` variable. It is an integer variable, and the method returns an integer, so our types match.

At this point, I can compile, run, and test the program. The compiler will flag missing arguments or type mismatch or missing return, so I don’t need to worry if it’s not perfect on the first try. We want to accept the natural iterative process of writing code, interpreting compiler errors, fixing errors, running code, fixing errors, etc.

It may seem like a lot of work to add this method, but the code is more readable and it encapsulates a calculation that I can reuse elsewhere. The improved readability may not be apparent at first, but if you compare the one method call with the original code, hopefully you can see that it is easier to glance at the code and know what it’s doing without needing to work out the full calculation or read any comments.

### Another Example

The `drawTree()` method also calculates the size of the tree based on how far down the screen it is. The trees at the bottom are given a larger size than trees towards the top, so they appear "closer" to the viewer. I'm going to make another method for this calculation. The y-coordinate is used to perform the calculation and it already has a value, so I will need to pass that value to the method as an argument. The `percent` variable is created locally, so I can simply move the variable to the new method. The size of the tree is a `float` value that is used to draw the tree, so the new method needs to return this value and the method header should reflect that:

```java
float getTreeSize(float y) {
  float percent = y / 500; // determines proximity of y-coordinate
  float h = 25 + percent * 150; // uses proximity to calculate tree size
  return h;
}

```
Remember the beauty of methods and local variables and parameters is that the variable names can be changed and they will not affect other methods, so I could write the above method like this instead:

```java
float getTreeSize(float yCoordinate) {
  float percentage = yCoordinate / 500; // determines proximity of y-coordinate
  float size = 25 + percentage * 150; // uses proximity to calculate tree size
  return size;
}

```

Variable names do not have to match, only the data types need to match. Finally, I’ll add the method call:

```java
// draw one tree at (x,y)
void drawTree(float x, float y) {
  int brown = color(92, 81, 59);

  int green = getTreeColor();

  float h = getTreeSize(y);

  fill(brown);
  rect(x, y, h / 5, h);
  fill(green);
  circle(x+h/10, y+h/10, h);
}

```

Compare this `drawTree()` method to the first one at the beginning of this section. By using descriptive method names, the process of the `drawTree()` method can be determined more quickly and I have meaningful labels for calculations that can be reused (and traced) more easily too.

## Final Word On Returns

One method can have multiple `return` statements in an if-else or inside a loop, but that is beyond the scope of this course. Understand that whenever a `return` is encountered, the method halts execution immediately and returns to the calling statement. Even if the method is in the middle of a loop or there is code after the `return`, the method ends execution immediately when a `return` statement is encountered.

A non-void method **must** include at least one `return` statement and **all** the `return` statements must include a value whose type matches the return type specified on the method header. 

Any `return` statement(s) in a `void` method is simply `return;`, with no value after it. A `void` method does not **have** to have a `return` statement at all because the method will automatically end at the closing brace, but sometimes they are added to end the method earlier under certain conditions. 

## Program Design

Using parameters and return values is a big step towards developing a more robust programming style that scales to larger and more complex programs. Relying on global variable for everything creates a lot of *cognitive load*--you need to remember which variable names you’ve used already, and be sure to avoid “collisions” between old variables and new ones. Defining methods and using parameters and return values is a powerful way of encapsulating different sections of your program and reducing the cognitive load you need to take on to successfully make changes to your code. Here are some general guidelines when writing methods:

Methods perform one task. If the task gets too big, consider breaking the task into smaller components and making multiple methods.
Method names should describe the action performed by the method.
Local variables and parameters are generally preferred to maintain encapsulation. Parameters are local variables that add variability to how the method executes. Other local variables can be created inside the method to aid in functionality.
Methods that return a value often perform some calculation and return the value to be used elsewhere in the program. The returned value is used immediately or stored in a variable for later use.
Global variables are best for constant values that do not change, like colors.

One of the most basic paradigms in programming is input-process-output. A program can be described by the input it receives, the processes it performs, and the output it produces. Methods within the program are components that follow this same paradigm; they receive input, they perform a task, and they produce output. In the statement, `int yellow = color(245, 220, 32);`, the color method receives the input 245, 220, and 32 and performs some process to produce the output of one integer value to represent the color yellow. As you practice writing and tracing methods, identify individual components within a program and continually ask yourself, “What is it doing? What input does it need? What type of output should it produce?”


### Future Applications

As mentioned earlier, methods are a key feature of "object-oriented" languages like Java and "functional" languages like Python. We will define classes and make objects in the next module and will be writing and using methods with parameters and return types extensively. 

For now, practice identifying methods and method calls in a program and practice tracing through the program manually. Use a “top-down” approach to tracing as we did when designing; trace the `setup()` and `draw()` methods sequentially to get an overview and then delve into individual methods to understand the details. Writing your own program and deciding or **knowing** when to add custom methods and whether to use parameters and/or return values requires a lot of iterative practice and trial and error.
