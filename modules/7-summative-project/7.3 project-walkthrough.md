---
title: Summative Project
authors: Jon Stapleton & Eva Anderson
date: 3/10/2022
type: page
---

:::youtube[A video tutorial showing how to complete the project]{#oXmKJ_tYg34}

# Summative Project

If you follow the project walkthrough below, you'll end up with a finished program that addresses the following prompt:

:::prompt
Create an interactive Processing program inspired by something from the natural world.
:::

This page contains a tutorial on how to complete the prompt, along with suggestions for people who would like to take on additional challenges. You are not required to use the project walkthrough! You may, of course, create your own original program that addresses the summative project prompt in your own way. This walkthrough is for folks who may not feel confident in their coding abilities yet, and for folks who are still getting used to Processing and Java.

## Walkthrough

This section contains details about how you might complete the the project described below:

:::prompt
  Create an empty landscape. When the user clicks the mouse, make a tree appear on the landscape
:::

Follow the instructions step-by-step to complete the project. Feel free to add some original work or make alterations as you go! By the end of this process, you'll have something that looks like this:

![The thing](TODO:)

You can look at the code for the GIF above [here](TODO:), but I recommend going through the steps below instead of skipping ahead.

### Step 1: Create the Landscape

::youtube[A tutorial on creating the landscape for your summative project]{#oXmKJ_tYg34}

To get started, set up an empty Processing sketch. Open Processing, and create the basic Processing structure that you learned about in [Module 5](TODO:):

:::code-example{TODO: url}
```java
// Create your variables here

void setup() {
  size(800, 600);
}

void draw() {

}
```
:::

Hit the `▶` button to check for errors before moving on!
Next, let's draw the "empty lanscape" from the prompt. There are a couple ways to accomplish this task using simple shapes. One method is to draw the sky as a blue `background`, and the ground as a green `rect` in front of it. First, we will create variables for the colors:
```javacolor sky = color(x, y, z);
color grass = color(x, y, z);
```
Then, we can use the `background()` method to draw the sky, and the `fill()` and `rect()` methods to draw the grass. Here's the whole thing:

:::code-example{TODO: url}
```java
// Create variables here
color sky = color(x,y,z);
color grass = color(x,y,z);

void setup() {
  size(800, 600);
  background(sky); // Draw the sky

  // Draw the ground
  fill(grass); // Change to green
  rect(0, 300, 800, 300);
}

void draw() {
  // Nothing yet!
}
```
:::

This is fine, but it's not all that visually interesting. What if we used `circle()` instead to make hills? (Note: the example below only shows the new `setup()` method, it does not show the complete program).

:::code-example{TODO: url}
```java
// ...
void setup() {
  size(800, 600);
  background(sky);

  // Draw the hills
  fill(grass);
  circle(0, 700, 600);
  circle(400, 700, 600);
  circle(800, 700, 600);
}
// ...
```
:::

Can you think of other improvements to make to the landscape?

:::prompt{challenge}
  For an additional challenge, add mountains in the background or randomly generated clouds from [Module 3](TODO:)!
:::


### Step 2: Drawing Tree Trunks, Part 1

::youtube[A tutorial on drawing tree trunks on the landscape when a user clicks the mouse]{#oXmKJ_tYg34}

Next, you need to add a tree to the scene when the user clicks the mouse button. There are a *lot* of ways to accomplish this, but I'm going to show one I think is neat.

First, let's draw a brown rectangle (for the trunk of a tree) *if* the user is pressing the mouse button. This will require us to use a new data type—`boolean`. It’s not all that important that you understand all the different ways to use this data type, just how to use it in an “if” statement like so:

```java
if (mousePressed == true) {
  // do something
}
```

In the code snippet above, the computer only executes the code inside the “if” statement if the built-in Processing variable contains the value `true` (which happens when the user is holding down a mouse button). Check out the following example, which shows the above snippet in context:

:::code-example{TODO: url}
```java
// Create variables here
color sky = color(x,y,z);
color grass = color(x,y,z);
color brown = color(x,y,z);

void setup() {
  size(800, 600);
  background(sky); // Draw the sky

  // Draw the ground
  fill(grass); // Change to green
  rect(0, 300, 800, 300);
}

void draw() {
  // New stuff!!
  if(mousePressed == true) {
      fill(brown);
      rect(400, 250, 20, 100);
  }
}
```
:::

Mission accomplished! But it would be nice to be able to plant multiple trees rather than just one. In the program above, the location of the rectangle is set by *literal* values, at `(400, 250)`. When the user clicks, it just adds the new tree in precisely the same location making it look like nothing changes.
To solve this problem, we can add the tree to the scene at the mouse coordinates rather than at `(400, 250)` (Note: the example below just shows the `draw()` method, not the entire program):

:::code-example{TODO: url}
```java
// ...
void draw() {
  if(mousePressed) {
      fill(brown);
      rect(mouseX, mouseY, 20, 100);
  }
}
```
:::

As you can see, there are two problems with this approach:

1. The program adds lots of trees when the user holds down the mouse button, which is even more obvious when the user drags the mouse while holding down the button
2. The top of the tree trunk aligns to the mouse coordinates rather than the bottom (this is more of an aesthetic problem, admittedly)

The second problem is the simplest one to solve, so I'll start there. Instead of using the built-in `mouseX` and `mouseY` variables to determine the location of the `rect()`, I'll *calculate* the `rect` coordinates *based on* the `mouseX` and `mouseY` values.

To get the correct `x` value, I'll take the value of `mouseX` and subtract half of the total `rect` width so the tree appears centered on the mouse: `mouseX-10`. To get the correct `y` value, I'll take the value of `mouseY` and subtract the total height of the `rect` so the "origin" of the `rect` appears above the mouse location: `mouseY-100`. Here's what we end up with:

:::code-example{TODO: url}
```java
//...
void draw() {
  if(mousePressed) {
      fill(brown);
      rect(mouseX-10, mouseY-100, 20, 100);
  }
}
```
:::

### Step 3: Drawing Tree Trunks, Part 2

The next problem is a little more complicated. The program above adds a tree trunk every frame in a location based on the `mouseX` and `mouseY` variables, which change as the user moves the mouse around. One way to solve this problem is to "freeze" the `x` and `y` values when the user clicks the mouse, so the location doesn't change while the program draws the tree trunk. Then, when the user *releases* the mouse button, the program will update the previously frozen `x` and `y` coordinates so the next time the user clicks, the tree appears at the new mouse location.
Start by creating new variables for the `rect` to use (Note: the example below only shows the parts of the code with new additions, and leaves out all the unchanged code you added from previous examples):

:::code-example{TODO: url}
```java
// Create variables here
int x = 300;
int y = 250;

// ...

void draw() {
  if(mousePressed) {
      fill(brown);
      rect(x-10, y-100, 20, 100);
  }
}
```
:::

This change puts the program back to where it was at the beginning--the tree always appears in the same spot, because the program never updates the `x` and `y` coordinates with new values. Next, add the code to update the `x` and `y` variables, but only when the mouse button *isn't* pressed:

:::code-example{TODO: url}
```java
//...
void draw() {
  if(mousePressed) {
      fill(brown);
      rect(x-10,y-100,20,100);
  } else {
      // update the rect location
      x = mouseX;
      y = mouseY;
  }
}
```
:::

It's working!

:::prompt{challenge}
  As a challenge, try changing the program so the user only can add a tree on the ground, rather than anywhere on the screen.
:::

### Step 4: Drawing Tree Trunks, Part 3

Let's make it a little fancy now--what if the tree "grew" where the user clicked the mouse, rather than just appearing? And what if the user could control the height of the tree by holding down the mouse?
To implement this new feature, we need to use a variable called `trunkHeight` to set the height of the `rect` the program draws when `mousePressed` is `true`:

```java
int x = 300;
int y = 250;
int trunkHeight = 100; // New variable!

// ...

void draw() {
  if (mousePressed) {
      fill(brown);
      // Use the height variable
      rect(x-10, y-trunkHeight, 20, trunkHeight);
  } else {
      x=mouseX;
      y=mouseY;
  }
}
```

To make the variable change value while the user holds the mouse button down, just increment the `trunkHeight` variable by `1` while the program draws the `rect`:

:::code-example{TODO: url}
```java
// ...
void draw() {
  if (mousePressed) {
      fill(brown);
      rect(x-10, y-trunkHeight, 20, trunkHeight);
      trunkHeight = trunkHeight + 1; // increase the height
  } else {
      x = mouseX;
      y = mouseY;
  }
}
```
:::

The only problem now is the tree height keeps getting bigger! Ideally, the tree height starts at `0` and grows from there, and then resets when the user releases the mouse button. All you need to do is set `trunkHeight` to zero when you "unfreeze" the `x` and `y` coordinates:

:::code-example{TODO: url}
```java
// ...
void draw() {
  if (mousePressed) {
      fill(brown);
      rect(x-10, y-trunkHeight, 20, trunkHeight);
      trunkHeight = trunkHeight + 1;
  } else {
      x = mouseX;
      y = mouseY;
      trunkHeight=0; // reset height
  }
}
```
:::

It works! Now the only thing left is to draw the branches of the tree.

:::prompt{challenge}
  As a challenge, try making the width of the tree grow along with the `trunkHeight`.
:::

### Step 5: Adding Branches to the Trees

::youtube[A video tutorial covering how to add branches to the trees from the previous sections]{#oXmKJ_tYg34}

The last thing to add to the program are the branches for the trees. Here's what I'd like to accomplish:

1. Add a green triangle on top of the tree trunk when the user releases the mouse button.
2. The taller the tree is, the more branches it should have.
3. If the tree is too small, it will be a "stump" and will not get any branches.

To help keep the code organized, I'm going to create a method that will take care of drawing the tree branches called `drawBranches`:

```java
// Create variables here
int x = 300;
int y = 250;
int height = 0;

color sky = color(x, y, z);
color grass = color(x, y, z);
color brown = color(x, y, z);

void setup() {
  size(800, 600);
  background(sky);
  fill(grass);
  rect(0, 300, 800, 300);
}

void draw() {
  if (mousePressed) {
      fill(brown);
      rect(x-10, y-trunkHeight, 20, trunkHeight);
      trunkHeight=trunkHeight+1;
  } else {
      x = mouseX;
      y = mouseY;
      trunkHeight = 0;
  }
}

// New function!!
void drawBranches() {
  // I'll add code here!
}
```

The first task is to add a green triangle to the top of the tree trunk when the user releases the mouse button. I'll add the code for drawing the triangle to the `drawBranches` method, and then I'll call the `drawBranches` method when the mouse button is released using the built-in `mouseReleased()` method:

:::code-example{TODO: url}
```java
// ...
// Define the method
void drawBranches() {
  color branch = color(x,y,z);
  fill(branch);
  triangle(x, y-150, x-50, y-50, x+50, y-50);
}

void mouseReleased() {
  // Call the method
  drawBranches();
}
```
:::

The `triangle` method is a little confusing to read. It takes 6 parameters, or 3 sets of `x` and `y` coordinates. Each coordinate represents the location of one point of the triangle. In the example above, the triangle has one point in the center (`x`) and `150` pixels above the top of the trunk (`y-150`), and two points `50` pixels above the base of the tree (`y-50`) to the right (`x+50`) and left (`x-50`) of center.

The program above has some problems. If the height of the tree is less than `50`, the branches "float" above the trunk. If the height is greater than `150` or so, the trunk peeks out above the branches! To solve this problem, I need to implement two features:

1. Avoid drawing branches for trees with a `trunkHeight` less than `50`.
2. Draw extra triangles up the trunk for trees with `trunkHeight` greater than `150`.

The first task just requires us to add an `if` statement to the `drawBranches()` method, checking the `trunkHeight` variable and skipping the `triangle` if the value is too small:

:::code-example{TODO: url}
```java
// ...
void drawBranches() {
  color branch = color(x,y,z);
  if(trunkHeight>=50) {
      fill(branch);
      triangle(x, y-150, x-50, y-50, x+50, y-50);
  }
}
```
:::

The second task is a little more complicated. In order to fill out the tree, we need to draw additional triangles to cover up the trunk that peeks out above the top of the first triangle. Sort of like this:

:::code-example{TODO: url}
```java
int x = 400;
int y = 300;
int trunkHeight = 175;

size(800, 600);
triangle(x, y-150, x+50, y-50, x-50, y-50);
// Another triangle 50px higher
triangle(x, y-200, x+50, y-100, x-50, y-100);
// Trunk in front
rect(x-10, y-trunkHeight, 20, trunkHeight);
```
:::

The number of triangles the program must draw depends on how tall the trunk is. If `trunkHeight` is greater than `200` or so, the program will need to draw *three* triangles instead of just two to cover up the entire trunk. In order to solve the problem, the program needs to do the following:

1. Figure out how many triangles to draw based on the value of `trunkHeight`
2. Draw the correct number of triangles, offsetting the location of each triangle by `50` pixels.

I'm going to start with the second part of the problem. First, I'm going to re-write the example above in the `drawBranches()` method using a loop instead of writing out the `triangle()` methods literally:

:::code-example{TODO: url}
```java
// ...
void drawBranches() {
  color branch = color(x, y, z);
  if(trunkHeight > 75) {
      fill(branch);
      // Draw two triangles
      int i=0;
      while(i&#x3C;2) {
          triangle(x, y-150-(i*50), x+50, y-50-(i*50), x-50, y-50-(i*50));
          i=i+1;
      }
  }
}
```
:::

In the example above, I've added an offset to all of the `y` coordinate values in the `triangle` method calculated based on the variable `i`, which increments in value each time the computer executes the loop. This has the effect of drawing multiple triangles in different locations with each loop!

Let's add a variable to control how many triangles to draw. Replace the `2` in the `while` Boolean expression with a variable:

:::code-example{TODO: url}
```java
void drawBranches() {
  color branch = color(x,y,z);
  if(trunkHeight > 75) {
      int numOfTris = 3 // set the number of loops
      int i=0;
      while(i&#x3C;numOfTris) {
          triangle(x, y-150-(i*50), x+50, y-50-(i*50), x-50, y-50-(i*50));
          i=i+1;
      }
  }
}
```
:::

Now, all I need to do is calculate how many triangles to draw! If the value of the `trunkHeight` variable is less than `125` or so, the program only needs to draw one triangle. If the value of `trunkHeight` is between `125` and `175`, the program should draw two. If `trunkHeight` is between `175` and `225`, then it needs to draw three, and so on. We can express the relationship between `trunkHeight` and the number of triangles the program should draw using an equation:

```java
int numOfTris = (trunkHeight-75)/50 + 1;
```

This expression is a little unintuitive--you might think to yourself: "How can we divide by 100 and still get an `int`? Won't this expression produce a decimal?"

You would be right, except that when we divide two `int`s in Java, the computer performs "integer division", discarding the remainder after division. So, in Java, `4/5` equals `0`, because of integer division.

The completed `drawBranches()` method looks like this:

:::code-example{TODO: url}
```java
void drawBranches() {
  if(trunkHeight > 75) {
      // Draw two triangles
      int numOfTris = (trunkHeight-75)/50 + 1;
      int i=0;
      while(i&#x3C;numOfTris) {
          triangle(x, y-150-(i*50), x+50, y-50-(i*50), x-50, y-50-(i*50));
          i=i+1;
      }
  }
}
```
:::

That's it! You've completed the prompt. Feel free to add in your own flair, or go back and complete some of the optional challenges! Once you're done, go to the next page to submit your work.

:::prompt{challenge}
  As a challenge, try adjusting the location and size of the branches triangle for trees where the height is less than 50 so the triangle scales in size with the tree!
:::


