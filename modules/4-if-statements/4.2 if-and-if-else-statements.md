---
title: Conditional Code
authors: Eva Anderson & Jon Stapleton
date: 4/23/2022
type: page
---

<!-- ::youtube[A video tutorial covering if and if-else statements]{#oXmKJ_tYg34} -->

# Conditional Code

So far, our programs only include statements for the computer to execute once, or multiple times within a loop. In this module, you’ll learn how to write programs that direct the computer to make a *decision* about what lines of code to run. You’ll add code blocks that the computer only executes when a certain condition is met. You can think of it as offering different choices or paths in your code where the computer uses a condition to decide which path to take. You’ll use "if" and "if-else" statements to create control structures which modify variables, simulate probabilities, or run methods conditionally, rather than sequentially.

## "If" Statement

A simple “if” statement is a control structure that contains code which the computer only executes when the value of a boolean expression is `true`. If the expression is `false`, the computer skips the code altogether. After the computer evaluates the “if” statement, the computer proceeds with reading and executing the code as usual.

Here is the syntax of an "if" statement:

```java
if (/*boolean expression is true*/)
{
	// do something
}
// else do nothing
```

Notice similarities in syntax between an “if” statement and a "while" loop (lack of semicolon on first line, parenthesis around boolean expression, curly braces). The "if" statement, however, does not repeat like a "while" loop; instead of running the code until the Boolean expression is `false`, it either runs or skips the code based on the expression and then moves on with the next line.

Here is a simple example that displays a different message depending on a random value stored in a variable. It simulates a coin toss; each time the computer executes the program, it displays either "HEADS" or "TAILS" on the screen. The output text (stored in the variable called `result`) starts as `”HEADS”`;  the computer changes it to `”TAILS”` by the "if" statement if the random value is a `0`.

```java
// runnable program; run it several times to try it out!
size(640,480);
String result = "HEADS";
int flip = (int)random(0,2); // 0 represents TAILS; 1 represents HEADS

if (flip == 0)	// if TAILS
{
   result = "TAILS"; // change variable to TAILS
}
// else it’s HEADS so do nothing (use initial value of variable)

circle(320, 240, 80);
fill(0);
text(result, 305, 245); // display result
```

*Note: Recall that random(0,2) produces a decimal value between 0.0 and 1.99999 and that casting this value with (int) truncates the decimal value, it does not round it. Therefore, flip either gets an integer value of  0 or 1.*

## "If-Else" Statement

With an "if-else" statement, both the `true` and the `false` path contain code blocks for the computer to execute. Here is the syntax of an "if-else" statement:

```java
if (/*boolean expression is true*/)
{
	// do something
}
else
{
// do something else
}
```

If the Boolean expression evaluates to `true`, the computer executes the first set of coded instructions; if the expression is `false`, it executes the next set of instructions. You can think of an “if-else” statement as instructing the computer to ask itself a question to determine if it should do one thing or another, but not both. After the “if-else” statement, the computer continues with the next statement.

You can rewrite the same HEADS or TAILS example using an if-else like so: 

```java
// runnable program
size(640,480);
String result;
int flip = (int)random(0,2); // 0 represents TAILS; 1 represents HEADS

if (flip == 0)	// if TAILS
{
   result = "TAILS"; // set variable to TAILS
}
else
{
   result = “HEADS”; // set variable to HEADS
}

circle(320, 240, 80);
fill(0);
text(result, 305, 245); // display result
```

This solution is neither better nor worse than the previous solution; it’s just different. 

In the next section, you’ll look at some more examples using "if" and "if-else".
