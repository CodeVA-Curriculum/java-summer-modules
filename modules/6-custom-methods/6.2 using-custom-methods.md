---
title: Using Custom Methods
author: Jon Stapleton & Eva Anderson
date: 4/20/2022
type: page
---

<!-- ::youtube[A video covering how to define and call custom methods]{#oXmKJ_tYg34} -->

# Using Custom Methods

You may have already realized that you can think about your code in pieces, with related commands "grouped" together. You may have even used comments to label these code "chunks", like sun and tree:

:::code-example{src="64727029" alt="The output of the program above, displaying a sun and a tree rendered using simple shapes"}
```java
// colors
int yellow = color(245, 220, 32);
int green = color(46, 179, 93);
int brown = color(92, 81, 59);

void setup() {
    size(800,600);
}

void draw() {
    // sun
    fill(yellow);
    circle(150, 150, 100);

    // tree
    fill(brown);
    rect(400, 300, 20, 100);
    fill(green);
    circle(410, 310, 100);
}
```
:::

I've organized the lines of code in the program above into "chunks" that belong together to make it easy for me to read the program and potentially identify problems as I'm testing. I *could* write it without organizing, as long as things are in the right order:

```java
fill(brown);
rect(400, 300, 20, 100);
fill(yellow);
circle(0, 0, 50);
fill(green);
circle(410, 310, 50);
```

...but this is difficult to read, and potentially difficult to debug (especially if my code gets a lot more complicated). Organizing your program into "functional" chunks is a great way to make sure things stay organized and easy to work with as you add more features.

## Organizing Code with Custom Methods

Java has a way to organize your code beyond just labeling your code with comments--you can make *methods* which contain related pieces of code. Recall that Processing's built-in methods (like `circle()` and `fill()`) are *command words* that direct the computer to perform some action. Additionally, recall that Processing also provides methods for *you* to define (like `setup()` and `draw()`), which the Processing environment calls for you under certain conditions. There is a third way to use methods: define and call them yourself.

Methods, generally, are keywords that reference a block of code. When a program contains a **call** to a particular method, the computer looks up the related code associated with that method's name and executes it. Sometimes, the method definition is hidden in a framework (like `circle()` or `color()`), other times you can see the method definition but not the call (like `draw()` or `mouseClicked()`). Additionally, you can define your *own* methods and then **call** them in your program. Here's the example from above, but with the code organized into customized methods defined in the program:

:::code-example{src="64727029" alt="The output of the program above, displaying a sun and a tree rendered using simple shapes"}
```java
// colors
int yellow = color(245, 220, 32);
int green = color(46, 179, 93);
int brown = color(92, 81, 59);

void setup() {
    size(800,600);
}

void draw() {
    // activate the code in the sun() definition
    sun();
    // activate the code in the tree() definition
    tree();
}

// definition for sun()
void sun() {
    fill(yellow);
    circle(0, 0, 50);
}

// definition for tree()
void tree() {
    fill(brown);
    rect(400, 300, 20, 100);
    fill(green);
    circle(410, 310, 50);
}
```
:::

The program now contains *four* method definitions: `draw()` and `setup()` are for the Processing environment, but `sun()` and `tree()` are new. They contain the code associated with drawing the sun and drawing the tree, respectively. These are the **method definitions** for `sun()` and `tree()`. The code also contains **calls** to these methods within the `draw()` method, making the `draw()` section of the code quite short!

## Defining and Calling Custom Methods

Creating and calling custom methods is pretty simple. Follow the steps below to organize your programs into methods:

1. First, identify a chunk of related code in your program and come up with a name (or "label") for that chunk. The name needs to be *unique* in the program; it shouldn't conflict with another variable name or method name.
2. Next, create a space for the method definition. I usually create these at the bottom of my program. Here's the syntax for a simple method definition, where label is the name youâ€™ve given to method:

```java
void label() {
    // code to execute on call
}
```

I chose to create a method called `tree()`. Since "tree" isn't a reserved keyword, it's okay to use it for a method name:

```java
void tree() {
    // code related to drawing a tree
}
```

3. Copy the code you identified in step #1 and paste it into the method definition area:

```java
void tree() {
    fill(brown);
    rect(400, 300, 20, 100);
    fill(green);
    circle(410, 310, 50);
}
```
4. Finally, call the method whenever you want that code to execute.

The example above uses a very simple section of code for explanatory purposes, but your method definitions can contain as much code as you like. It can even contain "if" statements, while loops, local variables, and pretty much anything else you might add to your program normally.

### Reviewing Scope

The "scope" issues you learned about in module 5 apply to your custom methods as well. If you want to use or modify a variable from your program in your method definition, you need to declare that variable *globally* at the top of your code, before `setup()`. If you initalize a variable in your method definition, the computer will discard that variable's name and value at the end of the method.

```java
// global variables, use these anywhere
int blue = color(157, 227, 250);
int grey = color(180, 180, 180);

void setup() {
    size(800,600);

    background(blue); // using the global "blue"
    
    int cloud_count = 0; // local variable

    while(cloud_count < 10) {
        cloud();
        cloud_count = cloud_count + 1;
    }

    // here, the program discards the
    // variable cloud_count, which means
    // the computer cannot use after this
    // point in the program.
}
void cloud()  {
    // local variables, only for use in the cloud() method
    float x = random(0, 800);
    float y = random(0, 600);
    int count = 0;
    while(count < 5) {
        // local variables
        float cx = random(x-75, x+75);
        float cy = random(y-50, y+ 50);
        
        fill(grey); // using the global "grey"
        
        noStroke();
        circle(cx, cy, 100);
        count = count + 1;

        // here, the program discards the variables
        // cx and cy
    }
    // here, at the end of the method, the program 
    // discards the variables x and y
}
```

If you have trouble understanding scope, a good temporary strategy is to just declare all your variables globally at the top of your program. Scope will become a lot more important as you develop larger, more complex programs. For now, global variables work fine.

### Why "Void"?

By now, you've seen the keyword `void` as a part of your method definitions a lot. This part of the method declaration will become a lot more important later on, but for now it's enough to say that the word `void` lets Java know that the method you've declared does not return any information. Consider these two uses of methods:

```java
// Does not return a value
circle(300, 300, 25);

// Returns a value
int red = color(255, 0, 0);
```

The `circle()` method works on its own, and just directs the computer to display a shape. The `color()` method is different, as you already know--it directs the computer to calcuate an `int` value based on the "red", "green", and "blue" values passed as arguments to the method call. Then, the computer returns that value back to the program, for storage in a variable (in this case).

Methods that do not return values, like `circle()` and your custom methods, use the keyword `void` in front of the method declaration to let the Java environment know that the program should not rely on the method to produce a value. If you try to use a `void` method as if it were designed to return a value, it will cause an error and your code will fail; try it out!:

```java
int v = circle(300, 300, 25);
```

Specifying methods as `void` is one way that Java programmers can use the language to make sure people use methods in the way they were intended to be used. This idea is very important in Java and other "strongly-typed" languages, and you'll learn a lot more about it as you develop your Java skills beyond these modules. For now, it's enough to know that you should declare your custom methods as `void` because they do not return any data.

## Why Create Methods At All?

You might ask yourself: "Why should I organize my code into methods? Creating commented labels works fine for me." That's a valid question! Sometimes, you may *not* need to organize your code into methods. However, as you create more complex programs, custom methods become very useful.

### Methods as a Planning Tool

Oftentimes, I find it useful to plan out a complex program by creating method calls, and then filling in the method definitions one by one. For example, I might envision a Processing project that includes mountains, clouds, trees, and a house. I could start by creating a simple program using method calls for each of these elements (note: this code will not run because there are not yet definitions for all the methods):

```java
void setup() {
    size(800,600);

    background(255); // sky
    clouds();
    mountains();
    trees();
    house();
}

void draw() {
    
}
```

This demonstrates a top-down design process, where the method calls act as a sort of "to-do" list. Organizing my program into methods *before* starting to write can help me focus my work on specific elements, rather than adding things willy-nilly. To start my work, I'll choose one method to work on, comment out the rest, and write the method definition for my chosen starting point:

```java
void setup() {
    size(800,600);

    background(255); // sky
    clouds();
    // mountains();
    // trees();
    // house();
}

void draw() {
    
}

void clouds() {
    // start writing cloud code here
}
```

### Methods to Aid Repetition

Methods are also useful when you need to repeat a section of code many times. You can think of methods as a sort of variable that stores *code* or a *process* rather than just storing a value. You can re-use the code stored in the method by calling it again. For example, I might use a method that contains code to draw a cloud in a random part of the screen:

```java
void cloud()  {
    float x = random(0, 800);
    float y = random(0, 600);
    int count = 0;
    while(count < 5) {
        float cx = random(x-75, x+75);
        float cy = random(y-50, y+ 50);
        fill(180);
        noStroke();
        circle(cx, cy, 100);
        count = count + 1;
    }
}
```

...and call that method multiple times to draw several clouds instead of just one:

:::code-example{src="64727065" alt="The output of the program above, displaying a randomly generated set of grey clouds"}
```java
void setup() {
    size(800,600);
    background(255);
    int cloud_count = 0;
    while(cloud_count < 10) {
        cloud(); // call the cloud method
        cloud_count = cloud_count + 1;
    }
}
```
:::

This technique will become more important later on, but for now it's a great way of simplifying repeated code.

### Future Applications

Additionally, methods have some powerful features that are beyond the scope of these modules, and it's important to familiarize yourself with the basics first before taking on these more complex implementations later. Methods can return values (as mentioned above) and they can also receive parameter values. Methods are a key feature of "object-oriented" languages like Java and "functional" languages like Python; it's a good idea to start thinking about code in terms of methods right now so you can begin learning more advanced topics later on!

