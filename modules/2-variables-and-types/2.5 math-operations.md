---
title: Mathematical Operations with Variables
authors: Eva Anderson & Jon Stapleton
date: 4/11/2022
type: page
---

<!-- ::youtube[A video tutorial covering mathematical expressions in Java]{#oXmKJ_tYg34} -->

# Mathematical Operations with Variables

In this module, you'll learn how to write mathematical expressions in Java. Mathematical expressions written in code express relationships, especially when you include variables as operands in your equations. Becoming comfortable writing equations in your program will allow you to use variables in a new way--not just as a label on a value, but as an expression of an idea, a pattern, or a relationship between two values that you can put to use in your code.

## Simple Operations

You can use the following mathematical **operators** to direct the compyuter to perform calculations with numeric primitive types (like `int` and `float`):

* `+` (add)
* `-` (subtract)
* `*` (multiply)
* `/` (divide)

When the computer encounters mathematical operations as it executes your code, it will resolve the operation before using the value it represents. For example, these two lines of code are functionally identical:

```java
ellipse(200, 200, 50, 50);
ellipse(100*2, 150+50, 100/2, 50);
```

The computer resolves each of the mathematical expressions in the parameter slots before executing the `ellipse` command on line 2.

You can also insert variables as operands in your mathematical expressions. For example, you might decide to have the computer generate a random number to determine the size of a tree, like you did on the previous page:

```java
size(800, 600);

// Create a random number
float size = random(50, 125);

// Draw the "trunk" of the tree
rect(400, 550, 20, 50);
// Draw the "branches"
circle(410, 550, size);
```

The problem with this example is that sometimes the tree branches will be *too big* for the height of the tree. Ideally, the bigger the `size` of the "branches", the *taller* the tree. To accomplish this, you can set the height and position of the `rect` based on the value of the `size` variable:

```java
rect(400, 600-size+20, 20, size+20);
circle(410, 550, size);
```

The code below uses both of these types of mathematical expressions (expressions with literal values, and expressions with variables). Consider each section of the code separately, and spend some time tracing the code.

:::code-example{src="64618467" alt="A randomly sized tree with a randomly colored square above it"}
```java
// colors
int blue = color(157, 227, 250); 
int green = color(46, 179, 93);
int brown = color(92, 81, 59);

// create the window
size(640, 480);
noStroke();
background(blue);

// Example 1:
textSize(6 * 4);
text("I <3 Math!", 25, 25);

// Example 2:
int R = 75;
int G = 100 - R;
R = 100;
int B = G + 50;
int mystery = color(R, G, B);
fill(mystery);
square(100, 100, 50);

// Example 3:
float x = 320;
float y = 350;
float radius = random(50, 150);
float trunkWidth = radius / 5;
float trunkHeight = radius;
fill(brown);
rect(x - 10, y + 10, trunkWidth, trunkHeight);
fill(green);
circle(x, y, radius);
```
:::

Note in Example 1 and 3, that a mathematical expression can be used directly as an argument for a method without storing the result in a variable (`textSize` and `rect`).

Note in Example 2 that we could not use R or G in a calculation until they had been created and given a value. Also, the variable G gets the value of `100` - R using the current value of R, so G gets the value of `25`. After that, R gets the value of `100` on the next line; this does NOT change the value of G.

In Example 3, notice that math operations are performed on the `x`, `y`, and `radius` values for the circle in order to set the parameter values for the `rect` method. Also, understand that in the statement `rect(x-10, y+10, trunkWidth, trunkHeight)` that `x` and `y` are not changed permanently because there is no assignment `=` operator. The variables `x` and `y` still hold the values `320` and `350` respectively.

> Why do you think the programmer chose to calculate the values of the `trunkWidth` and `trunkHeight` variables based on the value of the `radius` variable? What are each of these variables used for in the code?

## Complex Operations

You can use parenthesis `()` as grouping symbols and use multiple operators to perform more complex calculations. Hard brackets `[ ]` and curly braces `{}` are not valid grouping symbols. Java follows the usual order of operations commonly referred to as PEMDAS, with parenthesis evaluated first, then exponents (which we will not cover here), multiplication/division from left to right, and addition/subtraction from left to right. Here is an example with complex operations:

> **Practice:** See if you can trace through the code below and predict the three numbers the program will print to your console area. When you think you have a reasonable prediction, run the code and see if you are right!
>
> Recall from module 1 that tracing requires us to understand each line of code, one line at a time and that this process can be slow. As you trace each line of code, keep track of the current state (value) of each variable. Recall that a variable can only hold one value at a time and that the = sign is used to assign a new value to a variable.

```java
float x = 3;
float num = 10 - 7 * x;
println(num);

num = (10 - 7) * x;
println(num);

x = 6;
float y = 24;
num = y / x * 10;
println(num);
```

Notice that you still need to include the multiplication symbol to multiply a value by the result of a parentheses-bounded expression. For example, in math class you might write `(10-7)x`, but with Java you have to write it explicitly as `(10-7)*x`.