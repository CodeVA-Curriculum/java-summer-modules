---
title: The Processing Animation Loop
authors: Jon Stapleton & Eva Anderson
date: 4/12/2022
type: page
---

::youtube[A video tutorial covering the Processing animation loop]{#oXmKJ_tYg34}

# The Processing Animation Loop

So far, you've written Java programs that the computer executes *sequentially*, meaning that the computer "reads" each command one at a time, and performs each task in the order you've written it. You've also written some *looping* code using `while` loops. We will be building on this "looping" concept in this module as we discuss Processing's *animation loop*.

## Animation Loops Explained

Most programs that display moving images (think video games or other interactive visual applications) have a structure comprised of two sections: one section for code that should run once at the *beginning* of the program (i.e., right when you press "play"), and code that should loop over and over again *while the program is open*. This looping design pattern is called an *animation loop*.

Here's a sneak peek at what an animation loop looks like in the Processing environment. This program displays a circle "floating" across the screen from left to right:

:::code-example{src="TODO:}
```java
// declare variables
float x = 10; // a variable to keep track of circle's position

// code to run right away, once
void setup() {
  size(800, 600);
}

// code to loop over and over again, forever
void draw() {
  background(255);
  circle(x, 300, 20);
  x = x + 0.5;
}
```
:::

The definitions for the methods you are familiar with, like `circle` and `fill`, are hidden in the Processing framework, like a car engine is hidden under the hood. The `setup()` and `draw()` methods are different because Processing doesn't have definitions for those methods. Instead, *you* get to define them. When you run a Processing program, it calls the `setup` and `draw` methods automatically; if those methods aren’t defined, the code simply executes sequentially as we saw in previous programs.  You can imagine that Processing runs the following code behind the scenes whenever you run a program with a `setup` and `draw` method defined:

```java
setup();

while(true) {
    draw();
}
```

*Note: This is not functional code! It is just an explanation of how Processing calls the `setup()` and `draw()` methods "under the hood" after you press "play".*

## Processing's Animation Methods

The Processing environment has built-in code designed to facilitate animations: the `setup()` and `draw()` methods. Instead of *calling* these methods, however, you will be writing the code instructions for each method and Processing will call them. Here is how to create them:

### Step 1: Declare Your Variables

First, declare your variables at the top of your program, like so:

```java
float x = 10;
```

Same as usual.

### Step 2: Define the `setup()` Process

The next step is to add the `setup()` section to your code. Start by creating an empty `setup()` section with the word void, the method name, parenthesis, and curly braces, like this:

```java
float x = 10;

// setup section
void setup() {

}
```

Then, add the code that you'd like the computer to run at the *beginning* of the program, between the `{` and the `}` (in most cases, this will include the `size()` method, though you can add whatever you want in addition to that):

```java
float x = 10;

// setup section
void setup() {
    size(800,600);
}
```

The computer executes the code inside the `setup()` section *once* as soon as you press the "play" button.

### Step 3: Define the `draw()` Loop

Finally, add the code you'd like the computer to loop during the program's runtime to the `draw()` section. Same as the `setup()` process, start with an empty `draw()` section:

```java
float x = 10;

// setup section
void setup() {
    size(800,600);
}

// looping section
void draw() {

}
```

...then, add the code. The code below uses the code directly from the inside of the `while` loop from the example above:

:::code-example{src="TODO:"}
```java
float x = 10;

// setup section
void setup() {
    size(800,600);
}

// looping section
void draw() {
    circle(x, 300, 20);
    x = x + 0.5;
}
```
:::

Go ahead and try out the code! You'll notice that the circle still "smears" across the screen, but more smoothly this time!. To prevent the smear effect, you can apply a `background()` command before the `circle()` to "cover up" the previous circle on each iteration (similar to how a flip-book animation works):

:::code-example{src="TODO:"}
```java
float x = 10;

// setup section
void setup() {
    size(800,600);
}

// looping section
void draw() {
    background(255); // draw a white background
    circle(x, 300, 20);
    x = x + 0.5;
}
```
:::

Now you've arrived at the first example! You should read this code exactly like you would read the `while` loop example; the only difference is that instead of using a `while` loop, Processing uses the `draw()` method and also adds a delay so the loop doesn’t “freeze up” like it did in the `while` loop example above.

## How Processing Interprets This Code

The "sections" I've explained above, `setup()` and `draw()`, are methods just like `circle()` and `fill()`. The difference is that these methods are ones that you need to *define*, whereas the other methods were already defined and you only needed to *call* them.

Recall that a method like `circle()` or `color()` causes the computer to trigger a process, like outputting information to the screen to show a shape or calculating a number to represent a color. The Processing framework has defined methods for you, so you can just call the method instead of having to write code that manipulates the screen directly. This is sort of similar to how a car works, in a way--instead of having to inject fuel into the motor yourself, the car has a built-in pedal that takes care of that for you; all you have to do is press it. In the same way, you don't have to know how to write code that controls the screen or creates an application window; all you have to do is call the Processing methods that does that for you.

## Variable Scope

Now that you know how the `setup()` and `draw()` methods work, you might be wondering why *all* the code isn't in either of those two method definitions. Why are the variable declarations at the top?

The answer is this: if you declare a variable inside the `setup()` or `draw()` definitions, it will only be accessible *inside that method*, and the computer will *discard it after the method finishes running*. You can try this for yourself; the following code will produce an error:

```java
void setup() {
    float x = 10;
    size(800,600);
}
void draw() {
    background(255);
    circle(x, 300, 20);
    x = x + 0.5;
}
```

...because the way it's written now, the `x` variable is declared in `setup` so it’s only available inside the `setup()` method definition. This feature is called "scope"; variables can have *global* scope (meaning they are available across the whole program), or *local* scope (meaning they are available only in a specific section of the code).

For now, you should declare your variables at the top of your program *before* the `setup()` method. That way, your variables will have *global* scope and you won't run into any frustrating errors:

```java
// declare variables
float x = 10;

// setup
void setup() {
    size(800, 600);
}

// loop
void draw() {
    background(255);
    circle(x, 300, 20);
    x = x + 0.5;
}
```

> **Practice:** Try adding another variable and another "floating" shape to the example above! Be sure to declare your variable at the top of the code, and to add the code that displays the shape and updates your new variable to the `draw()` method definition. For an added challenge, add an if statement to reset the x value when it reaches the edge of the screen so the shape floats across the screen repeatedly.
