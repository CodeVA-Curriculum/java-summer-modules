---
title: Input with Processing
authors: Jon Stapleton & Eva Anderson
date: 4/13/2022
type: page
---

::youtube[A video turorial covering Processing input options]{#oXmKJ_tYg34}

# Input with Processing

This section of the module is all about creating interactive programs with Processing. Right now your Processing programs can include visuals and animations, but they don't respond to user input from the mouse or keyboard. By the end of this section, you'll be able to add user interaction to your programs, opening up a huge range of possible projects for the future.

You have already learned that Processing is a framework that provides tools for you to use as you craft software programs written in Java. Some of these tools are methods for you to call upon in your programs (e.g., `circle()`, `rect()`), and some of them are methods for you to define that Processing will then use in a specific, predetermined way. We will cover some additional tools like these in this section of the module, starting with the simplest options and working our way up to the more complex ones. The [Processing reference materials](TODO:) provide information about all of these options; be sure to follow the links below to learn more if you are interested!

## Simple Input with Processing Pre-Set Variables

The first set of options for user interaction come in the form of *pre-set variables* available globally in the Processing environment. These are not variables that you define or modify in your program; rather, the Processing environment modifies them *for* you, and then you can use them in your program. Two of these variables are called `mouseX` and `mouseY`. Try typing out the following Processing program in your environment:

:::code-example{src="TODO:"}
```java
void setup() {
    size(800,600);
}

void draw() {
    background(255); // make sure you include a background!
    fill(0); // set text color to black
    text(mouseX, 10, 10);
    text(mouseY, 10, 15);
}
```
:::

The program above directs the computer to display the values of the `mouseX` and `mouseY` variables to the screen so we can read them. You'll notice that I did *not* define these variables at the top of the program like I would normally. That's because Processing has *already defined them* "under the hood", and is modifying them *for* me based on the mouse movements in the application window.

Try it out! Move your mouse around, and watch how the values change!

You may have noticed that these values are numeric; this means you can pass them as parameters to many of the methods you are already familiar with:

:::code-example{src="TODO:"}
```java
void setup() {
    size(800,600);
}

void draw() {
    background(255);
    fill(0); // set text color to black
    text(mouseX, 10, 10);
    text(mouseY, 10, 15);

    // Draw a circle using the mouse coords
    circle(mouseX, mouseY, 25);
}
```
:::

There are lots of pre-set variables you can use in the Processing environment. Try replacing `mouseX` or `mouseY` in the `text()` methods from the code above to see what each of the variables below does. Note that you'll have to experiment with the mouse and keyboard a little to see changes to some of the variables:

* [`mouseButton`](https://processing.org/reference/mouseButton.html)
* [`mousePressed`](https://processing.org/reference/mousePressed.html)
* [`key`](https://processing.org/reference/key.html)
* [`keyPressed`](https://processing.org/reference/keyPressed.html)
* [`keyCode`](https://processing.org/reference/keyCode.html)

## Input Using Method Definitions

In addition to pre-set variables, Processing also provides some methods that you can provide definitions for which are associated with different user inputs. This input option allows you to write code that the computer will execute *once* when the user does something, like clicking the mouse or pressing a key. Try out the example below, which sets the value of a global variable whenever the user clicks the mouse: 

:::code-example{src="TODO:"}
```java
// declare global variable
int shapeColor = color(255, 255, 255);

// setup the program
void setup() {
    size(800,600);
}

// animation loop
void draw() {
    background(255);
    fill(shapeColor);
    circle(400, 300, 50);
}

// NEW INPUT METHOD DEFINITION!
// This code runs once when the user clicks the mouse
void mouseClicked() {
    int r = random(0, 255);
    int g = random(0, 255);
    int b = random(0, 255);
    shapeColor = color(r, g, b);
}
```
:::

You'll notice that the syntax for this input option is similar to the `setup()` and `draw()` animation loop syntax. The `mouseClicked()` method is much the same as the `setup()` and `draw()` methods--it is a method that *you*, and the coder must define. Then, Processing will activate it "under the hood" of the program under specific, predetermined conditions. Processing activates the code sequence inside the `mouseClicked()` method *once* when the user clicks a button on the mouse. Try it out!

There are a lot of other similar coder-defined Processing methods that work similarly. Try changing `mouseClicked()` in the example above to some of the other input method options to see how they work:

* [`keyPressed()`](https://processing.org/reference/keyPressed_.html)
* [`keyReleased()`](https://processing.org/reference/keyReleased_.html)
* [`keyTyped()`](https://processing.org/reference/keyTyped_.html)
* [`mouseDragged()`](https://processing.org/reference/mouseDragged_.html)
* [`mouseMoved()`](https://processing.org/reference/mouseMoved_.html)
* [`mousePressed()`](https://processing.org/reference/mousePressed_.html)
* [`mouseReleased()`](https://processing.org/reference/mouseReleased_.html)
* [`mouseWheel()`](https://processing.org/reference/mouseWheel_.html)

## Using Pre-Set Variables & Input Methods Together

You may find it useful to combine pre-set variables and input methods together. Here are a couple of examples:

:::code-example{src="TODO:"}
```java
// declare variables
int shapeColor = color(255, 255, 255);

void setup() {
    size(800,600);
}

void draw() {
    background(255);
    fill(0); // set text color
    text(key, 20, 20);

    // Draw shape
    fill(shapeColor);
    circle(400, 300, 80);
}

void keyTyped() {
    // Check which key was typed
    if(key == 'a') {
        // if 'a', change color to red
        shapeColor = color(200, 0, 0);
    } else if(key == 'b') {
        // if 'b' change color to blue
        shapeColor = color(0, 0, 200);
    }
}
```
:::

:::code-example{src="TODO:"}
```java
int direction = -1;
float x = 400;

void setup() {
    size(800,600);
}

void draw() {
    background(255);
    fill(0);
    text(direction, 20, 20);

    // Draw shape
    fill(200);
    circle(x, 300, 80);

    // Move the shape based on `direction`
    x = x + direction;
}

void mouseClicked() {
    // Check position of mouse
    if(mouseX <= 400) {
        // if on the left side, move left
        direction = -1;
    }
    if(mouseX > 400) {
        // if on the right, move right
        direction = 1;
    }
}
```
:::

> **Practice:** Try combining the two examples from this section of the module to create a program that 1) displays a circle over the user's mouse, and 2) changes the color of the shape randomly whenever the user clicks the mouse. For additional surprise, try removing the `background()` to see what happens!