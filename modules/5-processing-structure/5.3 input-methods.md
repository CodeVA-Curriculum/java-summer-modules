---
title: Input with Processing
authors: Jon Stapleton & Eva Anderson
date: 4/13/2022
type: page
---

::youtube[A video turorial covering Processing input options]{#oXmKJ_tYg34}

# Input with Processing

This section of the module is all about creating interactive programs with Processing. Right now your programs include visuals and animations, but they don't respond to user input from the mouse or keyboard. By the end of this section, you'll be able to add user interaction to your programs, opening up a huge range of possible projects for the future.

You have already learned that Processing is a framework that provides tools for you to use as you craft software programs written in Java. Some of these tools are methods for you to call upon in your programs (e.g., `circle()`, `rect()`), and some of them are methods for you to define that Processing will then use in a specific, predetermined way. We will cover some additional tools like these in this section of the module, starting with the simplest options and working our way up to the more complex ones. The [Processing reference materials](TODO:) provide information about all of these options; be sure to follow the links below to learn more if you are interested!

## Simple Input with Processing Pre-Set Variables

The first set of options for user interaction come in the form of *pre-set variables* available globally in the Processing environment. These are not variables that you define or modify in your program; rather, the Processing environment creates and modifies them *for* you, and then you can use them in your program. Two of these variables are called `mouseX` and `mouseY`. Try typing out the following Processing program in your environment:

:::code-example{src="TODO:"}
```java
void setup() {
    size(800,600);
}

void draw() {
    background(255); // make sure you include a background!
    fill(0); // set text color to black
    text(mouseX, 10, 10);
    text(mouseY, 10, 20);
}
```
:::

The program above directs the computer to display the values of the `mouseX` and `mouseY` variables to the screen so we can read them. You'll notice that these variables are not defined at the top of the program. That's because Processing has *already defined them* "under the hood", and is modifying them based on the mouse movements in the application window.

Try it out! Move your mouse around, and watch how the values change!

You may have noticed that these values are numeric; this means you can pass them as parameters to many of the methods you are already familiar with:

:::code-example{src="TODO:"}
```java
void setup() {
    size(800,600);
}

void draw() {
    background(255);
    fill(0); // set text color to black
    text(mouseX, 10, 10);
    text(mouseY, 10, 20);

    // Draw a circle using the mouse coords
    circle(mouseX, mouseY, 25);
}
```
:::

Try removing the `background(255);` line to see what happens!

The `circle()` "smears" across the screen when there's no `background()` because of the way the `draw()` method loops. You can think of each `draw()` iteration like a coat of paint. When you call `background(255);`, the computer adds a coat of white "paint" over the whole screen, covering up all the previous layers of paint. If you don't have a `background()` call, the old layers stay on the screen as `draw()` adds more shapes and text over time.

### More Pre-Set Variables

There are lots of pre-set variables you can use in the Processing environment. Try replacing `mouseX` or `mouseY` in the `text()` methods from the code above to see what each of the variables below does. Note that you'll have to experiment with the mouse and keyboard to see changes to some of the variables (and some of the changes disappear quickly so look at the values carefully):

* [`mouseButton`](https://processing.org/reference/mouseButton.html): An `int` variable that indicates which mouse button was last pressed
* [`mousePressed`](https://processing.org/reference/mousePressed.html): A `boolean` variable that indicates whether or not a mouse button is being held down by the user (*Note: this variable will throw an error if you add it to `text()` in the above example*)
* [`key`](https://processing.org/reference/key.html): A `char` variable containing the letter of the last key pressed by the user (`char` is beyond the scope of these modules, but you can still try it out!)
* [`keyPressed`](https://processing.org/reference/keyPressed.html): A `boolean` variable that indicates whether or not a mouse button is being held down by the user (*Note: this variable will throw an error if you add it to `text()` in the above example*)
* [`keyCode`](https://processing.org/reference/keyCode.html): An `int` variable that stores a numeric code referencing the last key the user pressed

## Input Using Method Definitions

In addition to pre-set variables, Processing also provides methods that you can provide definitions for, similar to the `setup` and `draw`methods, except these methods are associated with different user inputs. These new methods allow you to write code that the computer will execute *once*, automatically, when the user does something, like clicking the mouse or pressing a key. Try out the example below, which sets the value of a global variable whenever the user clicks the mouse: 

:::code-example{src="TODO:"}
```java
// declare global variables
float r = random(0, 255);
float g = random(0, 255);
float b = random(0, 255);
int shapeColor = color(r, g, b);

// setup the program
void setup() {
    size(800,600);
}

// animation loop
void draw() {
    background(255);
    fill(shapeColor);
    circle(400, 300, 50);
}

// NEW INPUT METHOD DEFINITION!
// This code runs once when the user clicks the mouse
void mouseClicked() {
    r = random(0, 255);
    g = random(0, 255);
    b = random(0, 255);
    shapeColor = color(r, g, b);
}
```
:::

You'll notice that the syntax for the `mouseClicked` method is similar to the `setup()` and `draw()` animation loop syntax. Processing calls this method under specific, predetermined conditions. In this case, Processing activates the code inside the `mouseClicked()` method *once* whenever the user clicks a button on the mouse. Try it out!

There are a lot of other coder-defined Processing methods that work similarly. Try changing `mouseClicked()` in the example above to some of the other input method options to see how they work:

* [`keyPressed()`](https://processing.org/reference/keyPressed_.html): Executes once when a user presses a key
* [`keyReleased()`](https://processing.org/reference/keyReleased_.html): Executes once when a user releases a key
* [`keyTyped()`](https://processing.org/reference/keyTyped_.html): Executes once when a user types a key (presses *and* releases)
* [`mouseDragged()`](https://processing.org/reference/mouseDragged_.html): Executes as the user drags the mouse
* [`mouseMoved()`](https://processing.org/reference/mouseMoved_.html): Executes once after the user moves the mouse
* [`mousePressed()`](https://processing.org/reference/mousePressed_.html): Executes once when the user presses a mouse button
* [`mouseReleased()`](https://processing.org/reference/mouseReleased_.html): Executes once when the user releases a mouse button
* [`mouseClicked()`](https://processing.org/reference/mouseClicked_.html): Executes when the user clicks (presses *and* releases) a mouse button
* [`mouseWheel()`](https://processing.org/reference/mouseWheel_.html): Executes when the user manipulates the mouse wheel

## Using Pre-Set Variables & Input Methods Together

You may find it useful to combine pre-set variables and input methods together. Here are a couple of examples:

:::code-example{src="TODO:"}
```java
// declare variables
int shapeColor = color(255, 255, 255);

void setup() {
    size(800,600);
}

void draw() {
    background(255);
    fill(0); // set text color
    text(key, 20, 20);

    // Draw shape
    fill(shapeColor);
    circle(400, 300, 80);
}

void keyTyped() {
    // Check which key was typed
    if(keyCode == 65) {
        // if 'a', change color to red
        shapeColor = color(200, 0, 0);
    } else if(keyCode == 66) {
        // if 'b' change color to blue
        shapeColor = color(0, 0, 200);
    }
}
```
:::

:::code-example{src="TODO:"}
```java
// move the mouse on either side of the circle to make it change direction
int direction = -1;
float x = 400;

void setup() {
    size(800,600);
}

void draw() {
    background(255);
    fill(0);
    text(direction, 20, 20);

    // Draw shape
    fill(200);
    circle(x, 300, 80);

    // Move the shape based on `direction`
    x = x + direction;
}

void mouseClicked() {
    // Check position of mouse
    if(mouseX <= x) {
        // if on the left side, move left
        direction = -1;
    }
    if(mouseX > x) {
        // if on the right, move right
        direction = 1;
    }
}
```
:::

> **Practice:** Try combining the two examples from this section of the module to create a program that 1) displays a circle over the user's mouse, and 2) changes the color of the shape randomly whenever the user clicks the mouse. Try removing the `background()` as well to see how it changes the program!

