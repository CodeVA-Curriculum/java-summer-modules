---
title: Traversing Arrays
authors: Jon Stapleton & Eva Anderson
date: 8/29/2022
type: page
---

# Traversing Arrays

In the last section, I stored five values with the same data type in one array instead of creating five variables to hold the values. It is common to write sequences of code that direct the computer to *travel* through an entire array to visit or access each individual element. This is called **traversing** the array. In this section, I'll show how to use a loop to traverse an array and access its elements. Then, without adding a *single line of code*, I can easily change the program to accommodate 100 bouncing balls instead of just five.

First, consider the main program with an array of five Ball objects from the previous section. (You can find the Ball class definition in the previous section)

<!-- bouncing-balls.gif -->
:::code-example{src="67708620" alt="The output of the program above" tabs}
::name[Main]
```java
// MAIN PROGRAM
Ball[] bouncingBalls;        // array declaration

// setup the program
void setup() {
    size(400,400);

    bouncingBalls = new Ball[5];  /// array initialization

    // put a Ball object in each container
    bouncingBalls[0] = new Ball();
    bouncingBalls[1] = new Ball();
    bouncingBalls[2] = new Ball();
    bouncingBalls[3] = new Ball();
    bouncingBalls[4] = new Ball();
}

// animation loop
void draw() {
    background(255);

    // display each Ball object in the array
    bouncingBalls[0].display();
    bouncingBalls[1].display();
    bouncingBalls[2].display();
    bouncingBalls[3].display();
    bouncingBalls[4].display();
}
```

::name[Ball]
```java
// Ball class used to make bouncing Ball objects
class Ball {
  float xCoord;
  float yCoord;
  int ballColor;
  float xDirection;
  float yDirection;
  
  // no-argument constructor
  Ball() {
    // randomize (x,y) coordinates
    xCoord = random(25,375);
    yCoord = random(25,375);

    // randomize color
    float r = random(0, 255);
    float g = random(0, 255);
    float b = random(0, 255);
    ballColor = color(r, g, b);
    
    // randomize direction ball moves at first
    float sign = random(0,1);
    if (sign < 0.5) {
      xDirection = 1;
    }
    else {
      xDirection = -1;
    }
    
    sign = random(0,1);
    if (sign < 0.5) {
      yDirection = 1;
    }
    else {
      yDirection = -1;
    }
  }
  
  void display() {
    // reverse direction if going off screen (left/right)
    if(xCoord < 25) {
      xDirection = -1 * xDirection;
    }
    else if(xCoord > 375) {
      xDirection = -1 * xDirection;
    } 
    
    // reverse direction if going off screen (top/bottom)
    if(yCoord < 25) {
      yDirection = -1 * yDirection;
    }
    else if(yCoord > 375) {
      yDirection = -1 * yDirection;
    }
    
    // move ball
    xCoord = xCoord + xDirection;
    yCoord = yCoord + yDirection;
    
    // draw ball
    fill(ballColor);
    circle(xCoord, yCoord, 50);
  }
}
```
:::

Notice that the lines of code to store `Ball` objects in the array are identical except for the index numbers:  0, 1, 2, 3, 4. The code to display all the objects in the array is also identical, except for the different indices. If I create a variable for the index number, then I can use a loop to perform each of these actions. By incrementing the index value inside the loop, I can have the computer access a different array element each time through the loop. 

## Syntax for Loop to Traverse an Array

This is the general syntax for using a while loop to traverse an array:

```java
int index = 0;
while (index < *sizeOfArray*)
{
    // do stuff with array
    index = index + 1;
}
```

You can change the variable name `index` to whatever you like, but it must be an `int` data type in order to be used as an array's index. You also need to replace *sizeOfArray* with the number of elements in the array. Inside the loop, you can write code to process each element in an array--initialize it, change it, or access it. Incrementing the `index` variable inside the loop prevents an infinite loop. You'll also use the `index` variable to access elements in the array you'd like the computer to traverse.

I'll add this loop to the `setup()` method in the main program so I can use it to store new `Ball` objects in the array:

```java
// MAIN PROGRAM
Ball[] bouncingBalls;        // array declaration

// setup the program
void setup() {
    size(400,400);

    bouncingBalls = new Ball[5];  /// array initialization

    // put a Ball object in each container with a loop
    int index = 0;
    while (index < 5)
    {
        bouncingBalls[index] = new Ball();
        index = index + 1;
    }
}

// animation loop
void draw() {
    background(255);

    // display each Ball object in the array
    bouncingBalls[0].display();
    bouncingBalls[1].display();
    bouncingBalls[2].display();
    bouncingBalls[3].display();
    bouncingBalls[4].display();
}
```

To accomplish this task, I:

1. Added the basic "while" loop to the code
2. Replaced *sizeOfArray* with the number of elements in the `bouncingBalls` array
3. Added a line of code to the inside of the loop that accesses the array and changed it to use the index variable: `[index]`
4. Deleted the old lines of code

I can compile and run the code at this point to make sure I have the correct syntax and logic. Then, I can add a "while" loop to replace the display code in the `draw()` method too. This time, I'll copy the "while" loop from the `setup()` method and modify it to call the `display()` method. 

```java
// MAIN PROGRAM
Ball[] bouncingBalls;        // array declaration

// setup the program
void setup() {
    size(400,400);

    bouncingBalls = new Ball[5];  /// array initialization

    // put a Ball object in each container with a loop
    int index = 0;
    while (index < 5)
    {
        bouncingBalls[index] = new Ball();
        index = index + 1;
    }
}

// animation loop
void draw() {
    background(255);

    // display each Ball object in the array with a loop
    int index = 0;
    while (index < 5)
    {
        bouncingBalls[index].display();
        index = index + 1;
    }
}
```

An alternate solution is to create a temporary variable to hold the next array element on each iteration and then call `display()` using the temporary variable:

```java
// MAIN PROGRAM
Ball[] bouncingBalls;        // array declaration

// setup the program
void setup() {
    size(400,400);

    bouncingBalls = new Ball[5];  /// array initialization

    // put a Ball object in each container with a loop
    int index = 0;
    while (index < 5)
    {
        bouncingBalls[index] = new Ball();
        index = index + 1;
    }
}

// animation loop
void draw() {
    background(255);

    // display each Ball object in the array with a loop
    int index = 0;
    while (index < 5)
    {
        Ball currentBall = bouncingBalls[index];  // temporary variable for each array element
        currentBall.display();
        index = index + 1;
    }
}
```

This is a style choice, although it's helpful for beginners because it separates the two steps of retrieving the object from the array and then using it to call a method. You can use whichever syntax you think is easier to read and trace.

## Array With 100 Elements

And, now, finally it's the moment we've been waiting for! By changing just three numbers in the code, I can display one hundred bouncing balls!

<!-- bouncing-balls-100.gif -->
:::code-example{src="67708650" alt="The output of the program above" tabs}
::name[Main]
```java
// MAIN PROGRAM
Ball[] bouncingBalls;    

// setup the program
void setup() {
    size(400,400);

    bouncingBalls = new Ball[100];  // CHANGED 5 TO 100

    // put a Ball object in each container
    int index = 0;
    while (index < 100)        // CHANGED 5 TO 100
    {
        bouncingBalls[index] = new Ball();
        index = index + 1;
    }
}

// animation loop
void draw() {
    background(255);

    // display each Ball object in the array
    int index = 0;
    while (index < 100)        // CHANGED 5 TO 100
    {
        Ball currentBall = bouncingBalls[index];  // temporary variable to hold array element
        currentBall.display();
        index = index + 1;
    }
}
```

::name[Ball]
```java
// Ball class used to make bouncing Ball objects
class Ball {
  float xCoord;
  float yCoord;
  int ballColor;
  float xDirection;
  float yDirection;
  
  // no-argument constructor
  Ball() {
    // randomize (x,y) coordinates
    xCoord = random(25,375);
    yCoord = random(25,375);

    // randomize color
    float r = random(0, 255);
    float g = random(0, 255);
    float b = random(0, 255);
    ballColor = color(r, g, b);
    
    // randomize direction ball moves at first
    float sign = random(0,1);
    if (sign < 0.5) {
      xDirection = 1;
    }
    else {
      xDirection = -1;
    }
    
    sign = random(0,1);
    if (sign < 0.5) {
      yDirection = 1;
    }
    else {
      yDirection = -1;
    }
  }
  
  void display() {
    // reverse direction if going off screen (left/right)
    if(xCoord < 25) {
      xDirection = -1 * xDirection;
    }
    else if(xCoord > 375) {
      xDirection = -1 * xDirection;
    } 
    
    // reverse direction if going off screen (top/bottom)
    if(yCoord < 25) {
      yDirection = -1 * yDirection;
    }
    else if(yCoord > 375) {
      yDirection = -1 * yDirection;
    }
    
    // move ball
    xCoord = xCoord + xDirection;
    yCoord = yCoord + yDirection;
    
    // draw ball
    fill(ballColor);
    circle(xCoord, yCoord, 50);
  }
}
```
:::

Voila! Behold the power of an array! I could get even fancier so it only takes *one* change if I create a global variable for the size of the array:

```java
// MAIN PROGRAM
Ball[] bouncingBalls;
int numBalls = 100;        // *new* variable for size of the array    

// setup the program
void setup() {
    size(400,400);

    bouncingBalls = new Ball[numBalls];  // CHANGED 100 to numBalls

    // put a Ball object in each container
    int index = 0;
    while (index < numBalls)        // CHANGED 100 to numBalls
    {
        bouncingBalls[index] = new Ball();
        index = index + 1;
    }
}

// animation loop
void draw() {
    background(255);

    // display each Ball object in the array
    int index = 0;
    while (index < numBalls)        // CHANGED 100 to numBalls
    {
        Ball currentBall = bouncingBalls[index];  // temporary variable to hold array element
        currentBall.display();
        index = index + 1;
    }
}
```

Now I can simply change the `numBalls` variable to increase or decrease the number of bouncing balls.

In the next section, we'll practice writing a program that creates and manages three different arrays. Some common errors with array traversals are listed below.

## Traversing Errors 

Here are somev common errors programmers encounter while writing code to traverse arrays with a loop:

* **IndexOutOfBounds Exception** This error is produced at run-time when the code attempts to use an invalid index for an array. In a loop that accesses multiple indices, one invalid index value with halt your whole program with this error.
* **Off By 1** This error occurs when one or more elements are inadvertently skipped. For example, with an array that hold 15 elements, a traversal should access indices 0 through 14. A code error that results in not accessing an element at index 0 or at index 14 is an example of an 'off by 1' error.
* **Infinite Loop** This error usually occurs if you forget to increment the index value inside the loop body. This will not generate a visible error, but can be identified when the output does not include all the images you expected.
* **Dead Loop** This error usually occurs with an incorrect condition on the loop, so the loop body is accidentally skipped entirely.  

---