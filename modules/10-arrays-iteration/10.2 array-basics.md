---
title: Array Basics
author: Eva Anderson
date: 8/21/2022
type: page
---

# Array Basics

Intro paragraph
Our earlier programs did not require us to store information for more than 5 or 10 variables at a time. We generated values (but didn't save them) to create static images. We only created a few object variables in our animated programs. More complex programs with animations, like a whole forest with moving trees or falling rain, require tens or hundreds of components to be redrawn every time the computer loops the `draw()` method. Arrays allow us to store and manage information for components with the same data type more easily than traditional variables.

A single array can store multiple values as long as the values have the same data type. For example, we can make an array to hold hundreds (or even thousands) of `Ball` objects. Or, we can make one to hold only integers. Let's look at an example that uses traditional variables to create and display five bouncing `Ball` objects.

<!-- gif -->
:::code-example{src="TODO:" alt="TODO:"}
::name[Ball]
```java
// Ball class used to make bouncing Ball objects
// A Ball is a circle (radius 50) with an x and y coordinate and a color
// Balls also have x and y direction variables (+1 or -1)
// to indicate the ball is moving left(-1)/right(+1) and up(-1)/down(+1)
class Ball {
  float xCoord;
  float yCoord;
  int ballColor;
  float xDirection;
  float yDirection;
  
  // no-argument constructor
  Ball() {
    // randomize (x,y) coordinates
    xCoord = random(25,375);
    yCoord = random(25,375);

    // randomize color
    float r = random(0, 255);
    float g = random(0, 255);
    float b = random(0, 255);
    ballColor = color(r, g, b);
    
    // randomize direction ball moves at first
    float sign = random(0,1);
    if (sign < 0.5) {
      xDirection = 1;
    }
    else {
      xDirection = -1;
    }
    
    sign = random(0,1);
    if (sign < 0.5) {
      yDirection = 1;
    }
    else {
      yDirection = -1;
    }
  }
  
  void display() {
    // reverse direction if going off screen (left/right)
    if(xCoord < 25) {
      xDirection = -1 * xDirection;
    }
    else if(xCoord > 375) {
      xDirection = -1 * xDirection;
    } 
    
    // reverse direction if going off screen (top/bottom)
    if(yCoord < 25) {
      yDirection = -1 * yDirection;
    }
    else if(yCoord > 375) {
      yDirection = -1 * yDirection;
    }
    
    // move ball
    xCoord = xCoord + xDirection;
    yCoord = yCoord + yDirection;
    
    // draw ball
    fill(ballColor);
    circle(xCoord, yCoord, 50);
  }
}
```

::name[Main]
```java
// MAIN PROGRAM
// declare global variables
Ball b1;
Ball b2;
Ball b3;
Ball b4;
Ball b5;

// setup the program
void setup() {
    size(400,400);
    b1 = new Ball();
    b2 = new Ball();
    b3 = new Ball();
    b4 = new Ball();
    b5 = new Ball();
}

// animation loop
void draw() {
    background(255);
    b1.display();
    b2.display();
    b3.display();
    b4.display();
    b5.display();
}
```
:::

In the program above, we directed the computer to produce random values to use in the `Ball` constructor for the x-coordinate, y-coordinate, color, and direction variables for each `Ball` object. The main program creates and displays five balls, and it would be easy to create another variable to add a sixth `Ball` object; but what if I wanted to add ten, twenty, or even one hundred more balls? I would need to make up to *nintey-five* additional variables to hold all the new balls. With an array, though, I can hold all 100 balls in one place. (For now, I'll show you how to implement an array to store just these five balls, then in the next section, I'll show you how to easily add *lots* more balls.)

## Creating an Array

To create an array, write the data type of the values to be stored with hard brackets `[ ]` and provide a name for the array. For example:

```java
int[] arrayOfIntegers;
Ball[] arrayOfBalls;
```

When reading the code, it helps to substitute the word "array" for the hard brackets, so read `int[]` as "integer array" or `Ball[]` as "Ball object array" or "array of Ball objects".

To initialize the array, I use the array's name with the assignment operator (`=`), the keyword `new` and the data type and hard brackets again, except this time I put a *number* inside the hard brackets to indicate how many storage containers I want in the array the array to accommodate.

```java
// Create the arrays
int[] arrayOfIntegers;
Ball[] arrayOfBalls;

// Initialize the arrays
arrayOfIntegers = new int[10]; // 10 integer containers
arrayOfBalls = new Ball[5]; // 5 Ball object containers
```

As with earlier variables, I can combine these two steps into one line:

```java
int[] arrayOfIntegers = new int[10]; // 10 integer containers
Ball[] arrayOfTrees = new Ball[5]; // 5 Ball object containers
```

The syntax for arrays can be a little difficult to read--here's a breakdown of each part, just to review:

* `int[]`: defines the type of the array variable
* `arrayOfIntegers`: the identifier (name) for the array
* `new int[10]`: Direct the computer to construct a new array in memory of size 10 and type `int`

The number inside the hard brackets sets the initial number of containers for the array. In Processing, I can call a method later to add an additional container, if necessary (we will cover this later, but it's not usually all that critical).

## Accessing an Array

Now that I have a set of containers, I need a way to access the individual containers so I can initialize the values in each one (and also access or change the values later). The computer automatically assigns each container a number, called an **index**. The first container in an array has an index of `0`, the second container has an index of `1`, and so on. I can use the array name with hard brackets (`[ ]`) and the index number to access the individual containers in the array.

The examples below show the syntax for initializing and accessing the individual containers in the arrays that I created earlier:

```java
arrayOfIntegers[0] = 5; // store 5 in the container at index 0
arrayOfIntegers[1] = 8; // store 8 in the container at index 1
arrayOfIntegers[2] = 17; // store 17 in the container at index 2 
print(arrayOfIntegers[0]); // print the integer value that's stored in the array at index 0 
int x = arrayOfIntegers[2]; // copy the integer value from array at index 2 and store in variable ‘x'
```

```java
arrayOfBalls[0] = new Ball(); // store a new Ball object in the container at index 0
arrayOfBalls[1] = new Ball(); // store a new Ball object in the container at index 1
arrayOfBalls[2] = new Ball(); // store a new Ball object in the container at index 2
arrayOfBalls[1].display(); // display the Ball object in array at index 1
Ball temp = arrayOfTrees[2]; // copy the Ball object from array at index 2 and store in ‘temp' variable
```

The actual values or objects stored in an array are referred to as **elements**. For example, the elements in `arrayOfIntegers` are integer values. So, I can use `arrayOfIntegers[2]` in a math calculation, store it in an integer variable, or pass it to a method that receives an integer parameter. Similarly, the element `arrayOfBalls[1]` is a Ball object, so I can use it as I would use any Ball object, like to call the `display()` method.

The individual elements within an array behave pretty much the same way as normal variables, despite the fact that the syntax to access them is much different. You can think of an array as a "pack" of variables that all share the same name.

## Example

Let's use the code at the beginning of this section and adapt it to use an array. I'll use the same steps outlined above:

1. Declare array
2. Initialize array
3. Store values in the array
4. Access or change the values in the array as needed

First, I'll remove all 5 variable declarations at the top and replace it with the array declaration. I'll call the array `bouncingBalls`.

```java
// MAIN PROGRAM
Ball[] bouncingBalls;        // array declaration

// setup the program
void setup() {
    size(400,400);
    b1 = new Ball();
    b2 = new Ball();
    b3 = new Ball();
    b4 = new Ball();
    b5 = new Ball();
}

// animation loop
void draw() {
    background(255);
    b1.display();
    b2.display();
    b3.display();
    b4.display();
    b5.display();
}
```

Next, I'll initialize the array in `setup()` to specify that I'll need five containers to hold `Ball` objects.

```java
// MAIN PROGRAM
Ball[] bouncingBalls;        // array declaration

// setup the program
void setup() {
    size(400,400);

    bouncingBalls = new Ball[5];  /// array initialization
    b1 = new Ball();
    b2 = new Ball();
    b3 = new Ball();
    b4 = new Ball();
    b5 = new Ball();
}

// animation loop
void draw() {
    background(255);
    b1.display();
    b2.display();
    b3.display();
    b4.display();
    b5.display();
}
```

Then, I'll use the array and an index number to store a new `Ball` object in each container:

```java
// MAIN PROGRAM
Ball[] bouncingBalls;        // array declaration

// setup the program
void setup() {
    size(400,400);

    bouncingBalls = new Ball[5];  /// array initialization

    // put a Ball object in each container
    bouncingBalls[0] = new Ball();
    bouncingBalls[1] = new Ball();
    bouncingBalls[2] = new Ball();
    bouncingBalls[3] = new Ball();
    bouncingBalls[4] = new Ball();
}

// animation loop
void draw() {
    background(255);
    b1.display();
    b2.display();
    b3.display();
    b4.display();
    b5.display();
}
```

And, finally, I'll change the code in `draw()` to display the `Ball` objects stored in the `bouncingBalls` array:

:::code-example{src="TODO:" tabs}
::name[Main]
```java
// MAIN PROGRAM
Ball[] bouncingBalls;        // array declaration

// setup the program
void setup() {
    size(400,400);

    bouncingBalls = new Ball[5];  /// array initialization

    // put a Ball object in each container
    bouncingBalls[0] = new Ball();
    bouncingBalls[1] = new Ball();
    bouncingBalls[2] = new Ball();
    bouncingBalls[3] = new Ball();
    bouncingBalls[4] = new Ball();
}

// animation loop
void draw() {
    background(255);

    // display each Ball object in the array
    bouncingBalls[0].display();
    bouncingBalls[1].display();
    bouncingBalls[2].display();
    bouncingBalls[3].display();
    bouncingBalls[4].display();
}
```

::name[Ball]
```java
// Ball class used to make bouncing Ball objects
// A Ball is a circle (radius 50) with an x and y coordinate and a color
// Balls also have x and y direction variables (+1 or -1)
// to indicate the ball is moving left(-1)/right(+1) and up(-1)/down(+1)
class Ball {
  float xCoord;
  float yCoord;
  int ballColor;
  float xDirection;
  float yDirection;
  
  // no-argument constructor
  Ball() {
    // randomize (x,y) coordinates
    xCoord = random(25,375);
    yCoord = random(25,375);

    // randomize color
    float r = random(0, 255);
    float g = random(0, 255);
    float b = random(0, 255);
    ballColor = color(r, g, b);
    
    // randomize direction ball moves at first
    float sign = random(0,1);
    if (sign < 0.5) {
      xDirection = 1;
    }
    else {
      xDirection = -1;
    }
    
    sign = random(0,1);
    if (sign < 0.5) {
      yDirection = 1;
    }
    else {
      yDirection = -1;
    }
  }
  
  void display() {
    // reverse direction if going off screen (left/right)
    if(xCoord < 25) {
      xDirection = -1 * xDirection;
    }
    else if(xCoord > 375) {
      xDirection = -1 * xDirection;
    } 
    
    // reverse direction if going off screen (top/bottom)
    if(yCoord < 25) {
      yDirection = -1 * yDirection;
    }
    else if(yCoord > 375) {
      yDirection = -1 * yDirection;
    }
    
    // move ball
    xCoord = xCoord + xDirection;
    yCoord = yCoord + yDirection;
    
    // draw ball
    fill(ballColor);
    circle(xCoord, yCoord, 50);
  }
}
```
:::

Yes, the array code is actually more lines of code and does not look much different than the original code, but when I add loops to process the array in the next section, then I can add a lot more objects without adding *any* new lines of code. Before you go, review some common errors with arrays below.

## Errors

Here are some common errors people often run into when they write code with arrays:

* **IndexOutOfBounds Exception** This error is produced at run-time when the code attempts to use an invalid index for an array. Given the array declaration:

```java
int[] arr = new int[10];
```

The valid index values or indices are 0 to 9. If we try to access `arr[10]` or `arr[15]` in the code, we will get an `IndexOutOfBounds Exception`. 

* **NullPointerException** The computer produces this error when the code asks it to use an array that has not been initialized. For example, consider this code:

```java
int[] numbers; 
numbers[0] = 100; 
```

The programmer forgot to initialize the `numbers` array with `numbers = new int[10];`, so the 2nd line produces a `NullPointerException` at run-time when it tries to store something in an array container before the array has been properly initialized.

* **NullPointerException** Since we are working with arrays of objects, we can also get this error for the object *inside* the array:

```java
Ball[] lotsaBalls = new Ball[20];
lotsaBalls[0].display();
```

The programmer forgot to initialize the container with a new `Ball` object, like `lotsaTrees[0] = new Ball()`, before displaying it. The 2nd line produces a NullPointerException at run-time because of this mistake.

* **Type Mismatch** As we've seen in prior modules, Java is "strongly typed", and each data type can be used in certain ways and not in others. It is our job, as programmers, to match types and use them properly. Here is an example of a type mismatch:

```java
Ball[] lotsaBalls = new Ball[20];
lotsaBalls[0] = 7;
```

Since the array `lotsaBalls` creates containers to hold `Ball` objects, it cannot store integer values, like `7`, in it. Processing throws a compiler error: "Cannot convert from int to Ball" to describe the type mismatch.

---
